/*
 * Streams.avail
 * Copyright © 1993-2015, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Streams"
Versions
	"1.0.0 DEV 2014-04-28"
Extends
	"Abstract Streams" =
	(
		"For each of⁇_do_",
		"For each…of_§do_",
		"an empty stream",
		"concatenate_",
		"drop from_until_",
		"drop from_while_",
		"drop«at most»?_from_",
		"element type",
		"empty stream",
		"empty-stream exception",
		"filter_by_",
		"map_through_",
		"stream",
		"stream of_",
		"take from_until_",
		"take from_while_",
		"take«at most»?_from_",
		"zip_",
		"_is empty",
		"_starts with_",
		"_'s⁇element type",
		"_'s⁇head",
		"_'s⁇tail",
		"«_‡zip»",
		"«_‡++»",
		"_'s⁇head method is_",
		"_'s⁇tail method is_"
	),
	"Bounded Streams" =
	(
		"_limited to_elements"
	),
	"Collection Streams" =
	(
		"a stream over_"
	),
	"Counting Streams" =
	(
		"a counting stream from_to_",
		"a counting stream from_to_by_"
	),
	"Delimited Streams" =
	(
		"_delimited by_",
		"_delimited by any of_"
	)
Uses
	"Foundation"
Body
	
/**
 * Answer a function that computes a prefix function.
 * 
 * @method "a stream prefix function"
 * @returns "[literal phrase ⇒ token, expression phrase ⇒ stream]→⊤"
 *          The requested function.
 */
Private method "a stream prefix function" is
[
	[
		elementPhrase : literal phrase ⇒ token,
		aStream : expression phrase ⇒ stream
	|
		Declare loop variable elementPhrase as
		[
		 	aStreamType ::= aStream's semantic type;
			aStreamType's element type's instance
		];
	]
]: [literal phrase ⇒ token, expression phrase ⇒ stream]→⊤;
	
/**
 * Stream iterating-loop that invokes {@param "action"} once for every
 * {@param "elementPhrase"} in {@param "aStream"} 
 * 
 * @macro "For each…of_§do_"
 * @category "Data Abstractions" "Streams"
 * @param "elementPhrase" "literal phrase ⇒ token"
 *        A {@type "literal phrase"} holding a synthetic {@type "literal token"}
 *        whose value is the name of the loop variable. The generated loop
 *        variable is given the strongest possible type based on of 
 *        {@param "aStream"}'s element type.
 * @param "aStream" "expression phrase ⇒ stream"
 *        An {@type "expression phrase"} that yields the stream to iterate over
 * @param "action" "block phrase ⇒ []→⊤"
 *        A zero-argument {@type "block phrase"} that represents the body of the
 *        loop.
 * @returns "send phrase ⇒ ⊤"
 * @category "Control Structures" "Loops" "Readers" "Data Abstractions"
 */
Macro "For each…of_§do_" is a stream prefix function,
[
	elementPhrase : literal phrase ⇒ token,
	aStream : expression phrase ⇒ stream,
	action : block phrase ⇒ []→⊤
|
	/* Build the block function */
	name ::= elementPhrase's token's lexeme;
	aStreamType ::= aStream's semantic type;
	blockType ::= [⊥]→(action's semantic type's return type);
	restrictions ::= semantic restrictions
		for method for $"For each of⁇_do_"
		given <aStreamType, blockType>;
	returnType : type := ⊤;
	From 1 to |restrictions| by 1 do
	[
		index : natural number
	|
		returnType := returnType
			∩ restrictions[index](aStreamType, blockType);
	];
	statements ::= action's statements;
	blockReturnType ::= cast statements
		into [t : phrase+ | t[|t|]'s semantic type]
		else [⊤];

	restricted send $"For each of⁇_do_" with
	<
		aStream,
		«[
			<cast scope map[name] into [d : argument phrase | d]>
		|
			statements
		] : blockReturnType»
	> : returnType
] : send phrase ⇒ ⊤;

/**
 * Answer a function that computes a body function.
 * 
 * @method "predicate body function(_,_)"
 * @param "anAtom" "atom" the atom that represents the function that the
 * 		macro builds
 * @param "baseReturnType" "type" the base return type of the method
 * @returns "[literal phrase ⇒ token, expression phrase ⇒ stream, 
 *   block phrase ⇒ []→ boolean]→send phrase ⇒ ⊤"
 *          The requested function.
 */
Private method "predicate body function(_,_)" is
[
 	anAtom : atom,
 	baseReturnType : type
|
	[
		elementPhrase : literal phrase ⇒ token,
		aStream : expression phrase ⇒ stream,
		predicateBlock : block phrase ⇒ []→ boolean
	|
		name ::= elementPhrase's token's lexeme;
		aStreamType ::= aStream's semantic type;
		blockType ::= [⊥]→(predicateBlock's semantic type's return type);
		restrictions ::= semantic restrictions
			for method for anAtom
			given <aStreamType, blockType>;
		returnType : type := baseReturnType;
			From 1 to |restrictions| by 1 do
			[
				index : natural number
			|
				returnType := returnType
					∩ restrictions[index](aStreamType, blockType);
			];
			statements ::= predicateBlock's statements;
			blockReturnType ::= cast statements
				into [t : phrase+ | t[|t|]'s semantic type]
				else [⊤];
		restricted send anAtom with
		<
			aStream, 
			«[
			  	<cast scope map[name] into [d : argument phrase | d]>
			 | 
			 	statements
			 ] : blockReturnType»
		> : returnType
	] : send phrase ⇒ ⊤
] : [literal phrase ⇒ token, expression phrase ⇒ stream, 
     block phrase ⇒ []→ boolean]→send phrase ⇒ ⊤;
	
/**
 * Answer a tuple containing the prefix of {@param "aStream"} that answered
 * {@method "true"} to {@param "predicateBlock"}.
 * 
 * @macro "take…from_§while_"
 * @param "elementPhrase" "literal phrase ⇒ token"
 *        A {@type "literal phrase"} holding a synthetic {@type "literal token"}
 *        whose value is the name of the loop variable. The generated loop
 *        variable is given the strongest possible type based on of 
 *        {@param "aStream"}'s element type.
 * @param "aStream" "expression phrase ⇒ stream"
 *        An {@type "expression phrase"} that yields the stream to iterate over
 * @param "action" "block phrase ⇒ []→⊤"
 * @param "predicateBlock" "block phrase ⇒ []→boolean"
 *        A zero-argument {@type "block phrase"} that represents the body of the
 *        predicate function.
 * @returns "send phrase ⇒ ⊤"
 * @category "Transformers" "Streams" "Data Abstractions"
 */
Macro "take…from_§while_" is a stream prefix function,
	predicate body function($"take from_while_", tuple);

/**
 * Answer a tuple containing the prefix of {@param "aStream"} that answered
 * {@method "false"} to {@param "predicateBlock"}.
 * 
 * @macro "take…from_§until_"
 * @param "elementPhrase" "literal phrase ⇒ token"
 *        A {@type "literal phrase"} holding a synthetic {@type "literal token"}
 *        whose value is the name of the loop variable. The generated loop
 *        variable is given the strongest possible type based on of 
 *        {@param "aStream"}'s element type.
 * @param "aStream" "expression phrase ⇒ stream"
 *        An {@type "expression phrase"} that yields the stream to iterate over
 * @param "action" "block phrase ⇒ []→⊤"
 * @param "predicateBlock" "block phrase ⇒ []→boolean"
 *        A zero-argument {@type "block phrase"} that represents the body of the
 *        predicate function.
 * @returns "send phrase ⇒ ⊤"
 * @category "Transformers" "Streams" "Data Abstractions"
 */
Macro "take…from_§until_" is a stream prefix function,
	predicate body function($"take from_until_", tuple);