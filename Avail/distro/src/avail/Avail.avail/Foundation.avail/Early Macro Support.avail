/*
 * Early Macro Support.avail
 * Copyright © 1993-2015, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Early Macro Support"
Versions
	"1.0.0 DEV 2014-04-28"
Uses
	"Bootstrap",
	"Early Conditionals",
	"Early Literals",
	"Very Early Definers"
Names
	/* Access to the parse map. */
	"parse map",
	"parse map:=_",

	/* Access to the bootstrap scope map. */
	"scope map",
	"scope map:=_",

	/* Shadowing. */
	"Require:_named_does not shadow"
Body

/**
 * Answer the current parse map. This is available only to macro definitions and
 * their prefix functions.
 *
 * @method "parse map"
 * @returns "{atom→any|}"
 */
Method "parse map" is
[
	current fiber[parse map key]
];

Semantic restriction "parse map" is
[
	{atom → any | whole number}
];

/**
 * Set the current parse map. This is available only to macro definitions and
 * their prefix functions.
 *
 * @method "parse map:=_"
 * @param "aMap" "{atom→any|}"
 * @returns "⊤"
 */
Method "parse map:=_" is
[
	aMap : {atom → any | whole number}
|
	current fiber[parse map key] := aMap;
];

/**
 * Answer the current scope map. This is available only to macro definitions and
 * their prefix functions.
 *
 * @method "scope map"
 * @returns "{nonempty string→declaration phrase|}"
 */
Method "scope map" is
[
	parse map[scope map key]
];

Semantic restriction "scope map" is
[
	{nonempty string → declaration phrase | whole number}
];

/**
 * Set the current scope map. This is available only to macro definitions and
 * their prefix functions.
 *
 * @method "scope map:=_"
 * @param "aMap" "{atom→any|}"
 * @returns "⊤"
 */
Method "scope map:=_" is
[
	aMap : {nonempty string → declaration phrase | whole number}
|
	parseMap ::= parse map + scope map key → aMap;
	current fiber[parse map key] := parseMap;
];

Method "private concatenate_" is
[
	tupleOfTuples : tuple of tuple
|
	Primitive 136;
] : tuple;

Semantic restriction "private concatenate_" is
[
	tupleOfTuples : tuple of tuple's type
|
	nonempty string
];

/**
 * Require that the proposed variable name does not shadow another local name.
 * Reject the current parse if it does. This method is intended to be invoked
 * only as a consequence of running a macro.
 *
 * @method "Require:_named_does not shadow"
 * @param "varRole" "nonempty string"
 *        A short description of the role of the proprosed variable, e.g.,
 *        "constant", "loop variable", etc.
 * @param "varName" "nonempty string"
 *        The proposed name of the new local.
 * @returns "⊤"
 */
Method "Require:_named_does not shadow" is
[
	varRole : nonempty string,
	varName : nonempty string
|
	If varName ∈ scope map then
	[
		Reject parse, expected:
			private concatenate
			<
				varRole,
				" ",
				varName,
				" to have a name that doesn't shadow another local declaration"
			>
	];
] : ⊤;
