/*
 * Primitives.avail
 * Copyright © 1993-2014, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Primitives"
Versions
	"1.0.0 DEV 2014-04-28"
Extends
	"Data Abstractions" = ("_'s⁇position", "_'s⁇position:=_")
Uses
	"Common",
	"File Access Rights",
	"File Names",
	"File Open Options" = (
		"read" → "read option",
		"write" → "write option"
		…),
	"Foundation",
	"Unit Testing"
Names
	/* File types. */
	"file",
	"readable file",
	"readable,⁇writable file",
	"writable file",

	/* File constructors. */
	"_opened«with alignment_»with options_«with file access rights_»",

	/* File attributes. */
	"_'s⁇name",
	"_is open",
	"_is closed",
	"_'s⁇alignment",
	"_'s⁇size",

	/* File I/O. */
	"a fiber reading_byte|bytes at_from_,⁇\
		\|on success doing_,⁇\
		\|on failure doing_,⁇\
		\|«forked at priority_»",
	"a fiber writing_at_to_,⁇\
		\|on success doing_,⁇\
		\|on failure doing_,⁇\
		\|«forked at priority_»",
	"a fiber synchronizing_with file system,then_else_priority_",
	"a fiber truncating_to_bytes⁇then_else_priority_",
	"Refresh_with file system",

	/* Directory I/O. */
	"a fiber creating a directory named_\
		\|«with file access rights_,⁇»\
		\|then_,⁇\
		\|else_,⁇\
		\|«forked at priority_»",
	"Create a directory named_«with file access rights_»",

	/* File cleanup. */
	"Using|using_,⁇do_",

	/* File exceptions. */
	"file-closed exception",
	
	/* File access rights. */
	"default directory access rights",
	"default file access rights",

	/* Unit testing. */
	"file primitives test suite"
Body

"name" is a new field atom;
"handle" is a new field atom;
"readable" is a new field atom;
"writable" is a new field atom;
"open state" is a new field atom;

/**
 * A file represents an open node within a file system.
 *
 * @type "file"
 * @supertype "object"
 */
Public class "file" extends object
	with fields
		handle : atom,
		public name : file name,
		readable : boolean,
		writable : boolean
	with mutable field
		open state : boolean;

/**
 * A readable file has been opened for reading.
 *
 * @type "readable file"
 * @supertype "file"
 */
Public class "readable file" extends file
	with field
		readable : true's type;

/**
 * A writable file has been opened for writing.
 *
 * @type "writable file"
 * @supertype "file"
 */
Public class "writable file" extends file
	with field
		writable : true's type;

/**
 * A readable, writable file has been opened for reading and writing.
 *
 * @type "readable,⁇writable file"
 * @supertype "readable file"
 * @supertype "writable file"
 */
Public class "readable,⁇writable file" extends readable file ∩ writable file;

/* Open a raw file handle. */
Private method
	"raw handle for file named_alignment_options_file access rights_" is
[
	name : string,
	alignment : whole number,
	options : {[0..9]|},
	rights : {[1..9]|}
|
	Primitive 160 (e : {
		invalid-path code,
		illegal-option code,
		operation-not-supported code,
		permission-denied code,
		I/O-error code,
		exceeds-vm-limit code}ᵀ);
	Raise an exception for e
] : atom;

_default_access_rights ::= "rw-r--r--" → file access rights;

/**
 * Answer the default {@type "file access right"}s with which files should be
 * created.
 * 
 * @method "default file access rights"
 * @returns "{file access right|}"
 */
Public method "default file access rights" is [_default_access_rights];

/**
 * Answer an abstraction of the file named {@param "n"} that is open for
 * reading. The file position is initially zero ({@code 0}).
 *
 * @method "_opened«with alignment_»with options_«with file access rights_»"
 * @param "fileName" "file name"
 * @param "optionalAlignment" "whole number?"
 *        The read/write buffer alignment that the underlying I/O operations
 *        should conform to whenever possible.  Individual transfers should
 *        still be allowed to span multiple alignment zones.  A zero is used to
 *        specify a suitable default provided by the primitive.
 * @param "optionSet" "{open option|}"
 *        The set of {@type "open option" options} to control how the file is to
 *        be opened.
 * @param "accessRights" "{file access right|}?"
 *        The initial {@type "file access right"}s to give the file in the event
 *        that a file is created by this operation.
 * @returns "file"
 *          The {@type file}.
 * @raises "permission-denied exception"
 *         If permission is denied to use the specified {@type "file name"}.
 * @raises "I/O exception"
 *         If an I/O error occurs for any reason.
 */
Public method
	"_opened«with alignment_»with options_«with file access rights_»" is
[
	fileName : file name,
	optionalAlignment : whole number?,
	optionSet : {open option|1..},
	optionalAccessRights : {file access right|}?
|
	optionNumbers ::=
		map optionSet through [opt : open option | opt → numeric code];
	accessRights ::= optionalAccessRights [1] else [_default_access_rights];
	accessRightNumbers ::= map accessRights
		through [r : file access right | r → numeric code];
	fileHandle ::= raw handle for file named fileName's name
		alignment optionalAlignment[1] else [0]
		options optionNumbers
		file access rights accessRightNumbers;
	a file with
		handle ::= fileHandle,
		name ::= fileName,
		readable ::= read option ∈ optionSet,
		writable ::= write option ∈ optionSet,
		open state := true
] : file;

/**
 * Strengthen the type with the file name and whether {@code "read"} and/or
 * {@code "write"} are specified.
 * 
 * @method "_opened with options_«with file access rights_»"
 * @restricts "file name's type"
 * @restricts "{open option|}'s type"
 * @restricts "{file access right|}'s type"
 */
Semantic restriction
	"_opened«with alignment_»with options_«with file access rights_»" is
[
	fileName : file name's type,
	optionalAlignment : natural number?'s type,
	optionSet : {open option|1..}'s type,
	accessRights : {file access right|}?'s type
|
	readableType : boolean's type := ⊥;
	writableType : boolean's type := ⊥;
	If |optionSet| ≠ ∞ then
	[
		/* Check each possibility, typically just one. */
		For each of optionSet's instances do
		[
			possibleSet : {open option|}
		|
			readableType :=
				readableType ∪ ((read option ∈ possibleSet)'s type);
			writableType :=
				writableType ∪ ((write option ∈ possibleSet)'s type);
		];
	]
	else
	[
		readableType := if read option ∈ optionSet's element type
			then [boolean]
			else [false's type];
		writableType := if write option ∈ optionSet's element type
			then [boolean]
			else [false's type];
	];
	If (readableType = false's type) ∧ [writableType = false's type] then
	[
		Reject parse, expected: "file open to specify read or write or both"
	];
	extend file with
		name : fileName,
		readable : readableType,
		writable : writableType
];

Method "_is readable" is [f : file | false] : boolean;
Method "_is readable" is [f : readable file | true];
Method "_is writable" is [f : file | false] : boolean;
Method "_is writable" is [f : writable file | true];
Public method "_is open" is [f : file | f's open state];
Public method "_is closed" is [f : file | ¬f's open state];

/**
 * @method "“_”"
 * @param "f" "file"
 * @returns "string"
 */
Stable method "“_”" is
[
	f : file
|
	s : string := if f is open then ["open"] else ["closed"];
	s := eject s ++ " file named " ++ “file's name”;
	If f is open then
	[
		s := eject s ++ " " ++
			if f is readable then
			[
				if f is writable then ["(readable, writable)"]
				else ["(readable)"]
			]
			else ["(writable)"];
	];
	s
];

/**
 * A file-closed exception is raised when a file operation is performed on a
 * closed file.
 *
 * @type "file-closed exception"
 * @supertype "I/O exception"
 */
Public explicit class "file-closed exception" extends I/O exception;

/**
 * Close the specified {@type "file"}.
 *
 * @method "Close_"
 * @param "f" "file"
 * @raises "file-closed exception"
 *         If {@param "f"} has already been closed.
 */
Method "Close_" is
[
	f : file
|
	If f is closed then [Raise a file-closed exception];
	[
		h : atom
	|
		Primitive 161 (e : invalid-handle code's type);
		Raise an exception for e
	] : ⊤ (f's handle);
	f's open state := false;
] : ⊤;

/**
 * Determine the alignment of the specified {@type "file"}.
 *
 * @method "_'s⁇alignment"
 * @param "f" "file"
 * @raises "file-closed exception"
 *         If {@param "f"} has already been closed.
 */
Method "_'s⁇alignment" is
[
	f : file
|
	If f is closed then [Raise a file-closed exception];
	[
		h : atom
	|
		Primitive 162 (e : invalid-handle code's type);
		Raise an exception for e
	] : natural number (f's handle)
] : natural number;

/**
 * Answer the current size of the specified {@type "file"}.
 *
 * @method "_'s⁇size"
 * @param "f" "file"
 * @returns "whole number"
 * @raises "I/O exception"
 *         If an I/O error occurs for any reason.
 * @raises "file-closed exception"
 *         If {@param "f"} has already been closed.
 */
Public method "_'s⁇size" is
[
	f : file
|
	[
		h : atom
	|
		Primitive 166 (e : {invalid-handle code, I/O-error code}ᵀ);
		Raise an exception for e
	] : whole number (f's handle)
] : whole number;

/* Truncate a file. */
Private method "private truncate_to_then_else_priority_" is
[
	fileHandle : atom,
	newSize : whole number,
	then : []→⊤,
	else : [{I/O-error code}ᵀ]→⊤,
	priority : byte
|
	Primitive 176 (e : {
		invalid-handle code,
		special-atom code,
		not-open-for-write code}ᵀ);
	Raise an exception for e
] : fiber→⊤;


/**
 * Set the size of the specified {@type "writable file"} to {@param "newSize"},
 * if {@param "newSize"} is less than the current size of {@param "f"},
 * otherwise do nothing.  Any data beyond the new size will be destroyed.
 *
 * @method "a fiber truncating_to_bytes⁇then_else_priority_"
 * @param "f" "writable file"
 * @param "newSize" "whole number"
 * @param "success" "[]→⊤"
 * @param "failure" "[{I/O-error code}ᵀ]→⊤"
 * @param "priority" "byte"
 * @returns "fiber"
 * @raises "invalid handle"
 *         If an I/O error occurs for any reason.
 * @raises "file-closed exception"
 *         If {@param "f"} has already been closed.
 * @raises "invalid handle"
 *         If {@param "f"} is not a file handle.
 * @raises "special atom"
 *         If {@param "f"} is a special atom.
 * @raises "not open for write"
 *         If {@param "f"} is a valid file handle, but not opened for write.
 */
Public method "a fiber truncating_to_bytes⁇then_else_priority_" is
[
	f : writable file,
	newSize : whole number,
	success : []→⊤,
	failure : [{I/O-error code}ᵀ]→⊤,
	priority : byte
|
	private truncate f's handle
		to newSize
		then success
		else failure
		priority priority
] : fiber→⊤;

Private method
	"private starting at_read_byte|bytes from_into_else_priority_" is
[
	start : natural number,
	bytesToRead : [1..∞],
	fileHandle : atom,
	into : [byte*]→⊤,
	else : [{I/O-error code}ᵀ]→⊤,
	priority : byte
|
	Primitive 164 (e : {
		invalid-handle code,
		special-atom code,
		not-open-for-read code,
		exceeds-vm-limit code}ᵀ);
	Raise an exception for e
] : fiber;

Public method
	"a fiber reading_byte|bytes at_from_,⁇\
		\|on success doing_,⁇\
		\|on failure doing_,⁇\
		\|«forked at priority_»" is
[
	bytesToRead : [1..∞],
	start : natural number,
	f : readable file,
	success : [byte*]→⊤,
	failure : [{I/O-error code}ᵀ]→⊤,
	optionalPriority : byte?
|
	private starting at start
		read bytesToRead bytes
		from f's handle
		into success
		else failure
		priority optionalPriority[1] else [current fiber's priority]
] : fiber;

Private method "private starting at_write_to_then_else_priority_" is
[
	start : natural number,
	bytesToWrite : byte+,
	fileHandle : atom,
	then : []→⊤,
	else : [{I/O-error code}ᵀ]→⊤,
	priority : byte
|
	Primitive 165 (e : {
		invalid-handle code,
		special-atom code,
		not-open-for-write code,
		exceeds-vm-limit code}ᵀ);
	Raise an exception for e
] : fiber;

Public method
	"a fiber writing_at_to_,⁇\
		\|on success doing_,⁇\
		\|on failure doing_,⁇\
		\|«forked at priority_»" is
[
	bytesToWrite : byte+,
	start : natural number,
	f : writable file,
	success : []→⊤,
	failure : [{I/O-error code}ᵀ]→⊤,
	optionalPriority : byte?
|
	private starting at start
		write bytesToWrite
		to f's handle
		then success
		else failure
		priority optionalPriority[1] else [current fiber's priority]
] : fiber;

/**
 * Block the current fiber until all writes buffered for the specified {@type
 * "file"} have been committed to the file system.  Unfortunately Avail's Java
 * substrate – and its sub-substrates in the OS – think it's ok to block a
 * thread just to wait for previously handed-off buffered write operations to
 * physically complete, defeating half of the purpose of asynchronous writes.
 * Sigh.
 * 
 * Fortunately, we have the "fileExecutor" pool of threads in the VM for just
 * that purpose – synchronous I/O operations.  We accomodate opening files,
 * renaming files and directories, and other similarly synchronous operations by
 * using the same dedicated pool.
 *
 * @method "Synchronize_with file system"
 * @param "f" "writable file"
 * @raises "I/O exception"
 *         If an I/O error occurs for any reason.
 * @raises "file-closed exception"
 *         If {@param "f"} has already been closed.
 */
Public method "a fiber synchronizing_with file system,then_else_priority_" is
[
	f : writable file,
	succeed : []→⊤,
	fail : [{I/O-error code}ᵀ]→⊤,
	priority : byte
|
	[
		handle : atom,
		succeed2 : []→⊤,
		fail2 : [{I/O-error code}ᵀ]→⊤,
		priority2 : byte
	|
		Primitive 169 (e : {
			invalid-handle code,
			special-atom code,
			not-open-for-write code}ᵀ);
		Raise an exception for e
	] : fiber (f's handle, succeed, fail, priority)
] : fiber;

Private method "private refresh_with file system" is
[
	fileHandle : atom
|
	Primitive 168 (e : {
		invalid-handle code,
		special-atom code,
		not-open-for-read code}ᵀ);
	Raise an exception for e
] : ⊤;

/**
 * Clear all buffers associated with the specified {@type "file"}.  Subsequent
 * reads from the file are guaranteed to fetch data at least as new as any
 * synchronize operations occurring prior to this refresh.
 * 
 * @method "Refresh_with file system"
 * @param "f" "readable file"
 *        The file whose buffers should be discarded.
 * @throws "invalid-handle code"
 */
Public method "Refresh_with file system" is
[
	f : readable file
|
	private refresh f's handle with file system;
] : ⊤;

/**
 * Perform the specified {@type "function" action}, passing {@param "f"} as the
 * argument. Whether the action completes normally or raises an exception,
 * {@method "Close_" close} {@param "f"}.
 *
 * @method "Using|using_,⁇do_"
 * @param "f" "file"
 *        A file, typically opened very near the send of this method. The
 *        argument position works especially well.
 * @param "action" "[⊥]→⊤"
 *        A function to apply to {@param "f"}.
 * @returns "⊤"
 *          The answer produced by applying {@param "action"} to {@param "f"}.
 */
Public method "Using|using_,⁇do_" is
[
	f : file,
	action : [⊥]→⊤
|
	Guard [action(f);]
	ensure [Close f;];
] : ⊤;

/**
 * @method "Using|using_,⁇do_"
 * @restricts "file's type"
 * @restricts "[⊥]→⊤'s type"
 */
Semantic restriction "Using|using_,⁇do_" is
[
	f : file's type,
	action : [⊥]→⊤'s type
|
	If ¬f ⊆ action[1] then
	[
		Reject parse, expected:
			format
				"function to accept the specified file \
				\|(but “①” is not a subtype of “②”)"
			with f, action[1]
	];
	action's return type
];

Private method
	"private create directory_file access rights_then_else_priority_"
is
[
	fn : string,
	rights : {[1..9]|},
	succeed : []→⊤,
	fail : [{file-exists code, I/O-error code, permission-denied code}ᵀ]→⊤,
	priority : byte
|
	Primitive 450 (e : {invalid-path code}ᵀ);
	Raise an exception for e
] : fiber→⊤;

_default_directory_access_rights ::= "rwxr-xr-x"→file access rights;

/**
 * Answer the default {@type "file access right"}s with which directories should
 * be created.
 * 
 * @method "default directory access rights"
 * @returns "{file access right|}"
 */
Public method "default directory access rights" is
[
	_default_directory_access_rights
];

/**
 * Create a new directory asynchronously. If the operation succeeds, then
 * eventually apply {@param "succeed"} in the answered {@type "fiber"}. If the
 * operation fails, then eventually apply {@param "fail"} to the error code in
 * the answered {@type "fiber"}.
 *
 * @method 
 *   "a fiber creating a directory named_\
 *      \|«with file access rights_,⁇»\
 *      \|then_,⁇\
 *      \|else_,⁇\
 *      \|«forked at priority_»"
 * @param "fn" "file name"
 *        The {@type "file name" name} of the target to create.
 * @param "optionalRights" "{file access right|}?"
 *        The {@type "file access right"}s with which to create the new
 *        directory. If not specified, then default to <code>rw-r--r--</code>.
 *        If the file system does not support {@type "file access right"}s, then
 *        just create the directory.
 * @param "succeed" "[]→⊤"
 *        What to do after the directory is successfully created.
 * @param "fail" "[{I/O-error code, permission-denied code}}ᵀ]→⊤"
 *        What to do with the error code if directory creation fails.
 * @param "optionalPriority" "byte?"
 *        The priority at which to launch the answerd {@type "fiber"}. If not
 *        specified, then default to the {@method "_'s⁇priority" priority} of
 *        the {@method "current fiber"}.
 * @returns "fiber→⊤"
 *          The {@type "fiber"} which will apply {@param "succeed"} or {@param
 *          "fail"}, as appropriate.
 * @raises "invalid-path exception"
 *         If {@param "fn"} does not represent a valid path.
 */
Public method
	"a fiber creating a directory named_\
		\|«with file access rights_,⁇»\
		\|then_,⁇\
		\|else_,⁇\
		\|«forked at priority_»"
is
[
	fn : file name,
	optionalRights : {file access right|}?,
	succeed : []→⊤,
	fail : [{file-exists code, I/O-error code, permission-denied code}ᵀ]→⊤,
	optionalPriority : byte?
|
	rights ::= map optionalRights[1] else [_default_directory_access_rights]
		through [r : file access right | r→numeric code];
	private create directory fn's name
		file access rights rights
		then succeed
		else fail
		priority optionalPriority[1] else [current fiber's priority]
] : fiber→⊤;

/**
 * Create a new directory synchronously.
 *
 * @method "Create a directory named_«with file access rights_»"
 * @param "fn" "file name"
 *        The {@type "file name" name} of the target to create.
 * @param "optionalRights" "{file access right|}?"
 *        The {@type "file access right"}s with which to create the new
 *        directory. If not specified, then default to <code>rw-r--r--</code>.
 *        If the file system does not support {@type "file access right"}s, then
 *        just create the directory.
 * @returns "⊤"
 * @raises "invalid-path exception"
 *         If {@param "fn"} does not represent a valid path.
 * @raises "permission-denied exception"
 *         If the operation is not permitted.
 * @raises "I/O exception"
 *         If an I/O error occurred.
 */
Public method "Create a directory named_«with file access rights_»" is
[
	fn : file name,
	optionalRights : {file access right|}?
|
	rights ::= map optionalRights[1] else [_default_directory_access_rights]
		through [r : file access right | r→numeric code];
	sem ::= a semaphore named "create " ++ fn's name with 0 out of 1 permits;
	killer : exception;
	Ignore: private create directory fn's name
		file access rights rights
		then [Signal sem;]
		else
		[
			e : {file-exists code, I/O-error code, permission-denied code}ᵀ
		|
			killer := an exception for e;
			Signal sem;
		]
		priority current fiber's priority;
	Await sem;
	If killer is assigned then [Raise killer];
] : ⊤;

/**
 * Disallow the general object constructor for file-related types.
 * 
 * @method "a|an_«with«_«:»?:=_‡,»»"
 * @forbids "1" "file name" "file" "readable file" "writable file"
 *          "readable,⁇writable file"
 */
Grammatical restriction "a|an_«with«_«:»?:=_‡,»»" is
	<{
		"file name",
		"file",
		"readable file",
		"writable file",
		"readable,⁇writable file"},
	∅,
	∅>;
