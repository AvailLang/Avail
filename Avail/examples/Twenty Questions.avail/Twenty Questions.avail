System Module "Twenty Questions"
Extends
Uses
	"Avail"
Names
Body

/**
 * 
 */
Explicit class "node" extends object
	with fields
		$str : string;

/**
 * 
 */
Class "question" extends node
	with fields
		$yes : ↑node,
		$no : ↑node;

/**
 * 
 */
root ::= new ↑node;
↓root ?= a node with
	$str := "Avail";

/**
 * 
 */
Method "Play Twenty Questions with input_and output_" is
[
	input : []→string,
	output : [string]→⊤
|
	count : integer := 0;
	response : string;
	curNode : ↑node := root;
	While [count ≤ 20 ∧ [↓curNode's type ⊆ question]]
	do
	[
	 	↑count++;
		output(↓curNode's str);
		response := input();
		If response = "n" ∨ [response = "no"] then [curNode := (cast ↓curNode into [q : question | q])'s no;]
		else if [response = "y" ∨ [response = "yes"]] then [curNode := (cast ↓curNode into [q : question | q])'s yes;];
	];
	output("Is it " ++ ↓curNode's str ++ "? ");
	response := input();
	answer : string;
	If response = "y" ∨ [response = "yes"] then [Print: "Yay!\n";]
	else if [response = "n" ∨ [ response = "no"]]
	then
	[
		output("I give up. What was it? ");
		answer := input();
		output("Okay. What is a question to distinguish it from "
			++ ↓curNode's str ++ "? ");
		response := input();
		tempNo : node := ↓curNode;
		tempYes : node := a node with $str := answer;
		↓curNode ?= a question with
				$str := response,
				$no := ↑tempNo,
				$yes := ↑tempYes;
	];
];

/**
 * 
 */
Method "Play in the console" is
[
	Play Twenty Questions with
		input [next line]
		and output [str : string | Print: str;];
] : ⊤;

Play in the console;
