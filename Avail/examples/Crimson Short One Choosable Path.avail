/*
 * Crimson Short One Choosable Path.avail
 * Copyright © 1993-2014, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Crimson Short One Choosable Path"
Versions
	"dev"
Uses
	"Avail",
	"Choosable Path"
Entries
	"Play Crimson Short One"
Body

Let the game context include
	boolean state
		"astronavigation exam requires revision" ✔ /
		"astronavigation exam does not require revision"
	whole number state
		"years served" (initially 15);

Let "Just Waking Up",
	"Studying is Hard",
	"Drive Room Maintenance",
	"Drive Room Diligence",
	and "Drive Room Disaster" be scenes;

Just Waking Up is described by
	"Yawwwn. You would've slept better if a certain gerbil-faced git hadn't
	kept you up all night with his uninspired guitar playing. Oh well. Time
	to face the day."
and has transitions
	to Studying is Hard
		described by
			"Your astronavigation exam is tomorrow. You really need to pass it
			so that you can become an officer and leave slob city behind.
			Better get cracking!"
	to Drive Room Maintenance
		described by
			"You should report for duty. You'd hate to get a reprimand!";

Studying is Hard
	is described by
		"You cover yourself with arcane mathematical formulae. Ha! You
		definitely won't write \"I am a fish\" on the test now!",
	upon entry does [Now astronavigation exam does not require revision;],
and has a transition
	to Drive Room Maintenance
		described by
			"Okay, guess it's time to report for duty.";

Drive Room Maintenance is described by
	"According to the duty roster, it's time to perform maintenance on the
	drive plate. Sounds like real work. Blech."
and has transitions
	to Drive Room Disaster
		described by
			"Poke around, but don't bother double checking the drive plate.
			Everything will be fine, and you have other things to do.",
	to Drive Room Diligence
		contingent on [astronavigation exam does not require revision]
		described by
		[
			time ::= years served + 5;
			"Better double check the drive plate. You'd hate for there to be
			some kind of freakish nuclear holocaust to interfere with
			acquiring your " ++ “time” ++ " years long service medal."
		];

Drive Room Disaster is described by
	"You go about your day until you are suddenly transformed into a small pile
	of white dust by a nuclear explosion. \"Gazpacho soup…\"";

Drive Room Diligence is described by
	"It's a good thing that you checked the drive plate! It needed serious
	attention. Were it not for your diligence, there might have been a nuclear
	holocaust.\n\nCongratulations!";

Let Just Waking Up be the opening scene;

Verify story;

Method "Play Crimson Short One" is
[
	Play a game,
		creating a context with [a game context],
		interacting with standard console interface;
];
