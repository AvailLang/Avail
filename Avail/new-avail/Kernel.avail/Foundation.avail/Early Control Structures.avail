/*
 * Cast.avail
 * Copyright © 1993-2012, Mark van Gulik and Todd L Smith.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

System Module "Early Control Structures"
Versions
	"dev"
Extends
Uses
	"Assertions",
	"Bootstrap",
	"Early Literals",
	"Early Type Support"
Names
	"(_)",
	"cast_into_else",
	"cast_into_"
Body

/**
 * Establish grammatical boundaries around an expression. This serves to assist
 * the compiler in disambiguating expressions. Used well, it also improves
 * readability of source text by programmers.
 *
 * Parameters:
 *    value - A value, usually produced by a method send expression.
 * Returns:
 *    The argument.
 */
Method "(_)" is
[
	value : any
|
	value;
];

/**
 * A parenthesized expression should have the same static return type that it
 * would were the parentheses not present.
 *
 * Parameters:
 *    value -
 * Returns:
 *    The argument.
 */
Semantic restriction "(_)" is
[
	value : type
|
	value;
];

/**
 * Establish a conditional expression whose static return type is the type union
 * of its two possible functions' return types.
 *
 * Parameters:
 *    predicate - The predicate's type.
 *    trueFunction -
 *    falseFunction -
 * Returns:
 *    The type union of the return types of `trueFunction` and `falseFunction`.
 */
Semantic restriction "if_then_else_" is
[
	predicate : boolean's type,
	trueFunction : nullary procedure's type,
	falseFunction : nullary procedure's type
|
	trueFunction's return type ∪ falseFunction's return type;
];

/**
 * Strengthen the static return type to that of the function to invoke when the
 * predicate is true (which it is).
 *
 * Parameters:
 *    tautology - The type of `true`.
 *    trueFunction -
 *    falseFunction -
 * Returns:
 *    The return type of `trueFunction`.
 */
Semantic restriction "if_then_else_" is
[
	tautology : true's type's type,
	trueFunction : nullary procedure's type,
	falseFunction : nullary procedure's type
|
	trueFunction's return type;
];

/**
 * Strengthen the static return type to that of the function to invoke when the
 * predicate is false (which it is).
 *
 * Parameters:
 *    tautology - The type of `false`.
 *    trueFunction -
 *    falseFunction -
 * Returns:
 *    The return type of `falseFunction`.
 */
Semantic restriction "if_then_else_" is
[
	contradiction : false's type's type,
	trueFunction : nullary procedure's type,
	falseFunction : nullary procedure's type
|
	falseFunction's return type;
];

/**
 * If the function `accept` accepts `value` as an argument (based on its
 * runtime type), then invoke `accept` with `value`. Otherwise, invoke `reject`
 * instead.
 * 
 * This method effects a dynamic type cast of `value` to the type of `accept`'s
 * only parameter. While type casts are occasionally necessary even in runtime
 * code, this facility is primarily intended for use by semantic restriction
 * functions. Used in this way it leads to a stronger static type system.
 *
 * Parameters:
 *    value - A value.
 *    accept - The function to invoke if `value` is a subtype of the its
 *       parameter type.
 *    reject - The function to invoke if `value` is not a subtype of the
 *       parameter type of `accept`.
 */
Method "cast_into_else_" is
[
	value : any,
	accept : function accepting <⊥> and returning ⊤,
	reject : nullary procedure
|
	if value's type ≤ accept's type[1] then
	[
		invoke accept with <value>;
	]
	else reject;
] : ⊤;

/**
 * If the function `accept` accepts `value` as an argument (based on its
 * runtime type), then invoke `accept` with `value`. Otherwise, cause an
 * assertion failure. This is obsolete once exceptions are available, and should
 * not be exported beyond this package.
 *
 * Parameters:
 *    value - A value.
 *    accept - The function to invoke if `value` is a subtype of the its
 *       parameter type.
 */
Method "cast_into_" is
[
	value : any,
	accept : function accepting <⊥> and returning ⊤
|
	if value's type ≤ accept's type[1] then
	[
		invoke accept with <value>;
	]
	else
	[
		Shouldn't happen;
	];
] : ⊤;

/**
 * Strengthen the return type of `cast_into_else` so that it may be used as a
 * value-producing expression. If the type cast is unnecessary, then reject the
 * current parse.
 *
 * Parameters:
 *    value -
 *    accept -
 *    reject -
 * Returns:
 *    The type union of the return types of `accept` and `reject`.
 */
Semantic restriction "cast_into_else_" is
[
	value : type,
	accept : (function accepting <⊥> and returning ⊤)'s type,
	reject : nullary procedure's type
|
	if value ≤ accept[1] then
	[
		messageTuple ::= concatenate <
			"necessary cast expression, but (",
			primitive description of value,
			" ≤ ",
			primitive description of accept[1],
			")">;
		cast messageTuple into
		[
			message : string
		|
			Reject parse, expected: message;
		];
	];
	accept's return type ∪ reject's return type;
];

/**
 * Strengthen the return type of `cast_into_` so that it may be used as a
 * value-producing expression. If the type cast is unnecessary, then reject the
 * current parse.
 *
 * Parameters:
 *    value -
 *    accept -
 * Returns:
 *    The return type of `accept`.
 */
Semantic restriction "cast_into_" is
[
	value : type,
	accept : (function accepting <⊥> and returning ⊤)'s type
|
	if value ≤ accept[1] then
	[
		messageTuple ::= concatenate <
			"necessary cast expression, but (",
			primitive description of value,
			") ≤ (",
			primitive description of accept[1],
			")">;
		cast messageTuple into
		[
			message : string
		|
			Reject parse, expected: message;
		];
	];
	accept's return type;
];

/* Nesting parentheses directly is bad style, and leads to confusing
 * expressions, so forbid it.
 */
invoke
	[
		names ::= cast <"(_)">→set into [ t : {string|whole number} | t; ];
		Grammatical restriction names is <names>;
	]
with <>;
