/*
 * Assertions.avail
 * Copyright © 1993-2012, Mark van Gulik and Todd L Smith.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

System Module "Assertions"
Versions
	"dev"
Uses
	"Bootstrap"
Names
	"Assert:_",
	"Assert:_(_)",
	"Shouldn't happen"
Body

/**
 * Early assertion mechanism. This is obsolete once exceptions are available,
 * and should not be exported beyond this package.
 * 
 * Parameters:
 *    predicate - `true`.
 */
Method "Assert:_" is
[
	predicate : boolean
|
	/* Do nothing. */
]  : ⊤;

/**
 * Early assertion mechanism. This is obsolete once exceptions are available,
 * and should not be exported beyond this package.
 * 
 * Print
 *    "An assertion failed.\n"
 * to standard output and then terminate the current fiber.
 *
 * Parameters:
 *    predicate - `false`.
 */
Method "Assert:_" is
[
	predicate : false's type
|
	message ::= "An assertion failed.\n";
	Print: message;
	Terminate: message;
] : ⊥;

/**
 * Strengthen the return type to ⊥ iff the assertion statement is statically
 * identifiable as a contradiction.
 *
 * Parameters:
 *    contradiction - The type of `false`.
 * Returns:
 *    ⊥.
 */
Semantic restriction "Assert:_" is
[
	contradiction : false's type's type
|
	⊥;
];

/**
 * Early assertion mechanism. This is obsolete once exceptions are available,
 * and should not be exported beyond this package.
 *
 * Parameters:
 *    predicate - `true`.
 *    message - Text describing the assertion failure. Ignored.
 */
Method "Assert:_(_)" is
[
	predicate : boolean,
	message : string
|
	/* Do nothing. */
] : ⊤;

/**
 * Early assertion mechanism. This is obsolete once exceptions are available,
 * and should not be exported beyond this package.
 *
 * Print
 *    "An assertion failed: \[`message`]\n."
 * to standard output and then terminate the current fiber.
 *       
 * Parameters:
 *    predicate - `false`.
 *    message - Text describing the assertion failure.
 */
Method "Assert:_(_)" is
[
	predicate : false's type,
	message : string
|
	Print: "An assertion failed: ";
	Print: message;
	Print: "\n";
	Terminate: message;
] : ⊥;

/**
 * Strengthen the return type to ⊥ iff the assertion statement is statically
 * identifiable as a contradiction.
 *
 * Parameters:
 *    contradiction - The type of `false`.
 *    message -
 * Returns:
 *    ⊥.
 */
Semantic restriction "Assert:_(_)" is
[
	contradiction : false's type's type,
	message : string's type
|
	⊥;
];

/**
 * Declare that the code path that called this method should never be executed.
 * It results in an assertion failure. This is obsolete once exceptions are
 * available, and should not be exported beyond this package.
 */
Method "Shouldn't happen" is
[
	Assert: false
		("This should not be possible; debug the VM and/or system library.");
] : ⊥;
