/*
 * Function Tests.avail
 * Copyright © 1993-2013, Mark van Gulik and Todd L Smith.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

System Module "Function Tests"
Versions
	"dev"
Uses
	"Common",
	"Foundation",
	"Unit Testing"
Names
	"function test suite"
Body

_testSuite ::= $"function test suite";

/**
 * Answer the test suite identifier of the function test suite.
 *
 * @method "function test suite"
 * @returns "atom"
 *          The requested test suite identifier.
 */
Public method "function test suite" is [ _testSuite ];

New test suite function test suite extends foundation test suite;

Test "partial application basics" in function test suite is
[
	f ::= [a : integer, b : string, c : float | "ok"];
	g ::= f(_,"foo",_);
	Require: g's type[1] = integer;
	Require: g's type[2] = float;
	Require: g(10, 1.2→float) = "ok";

	h ::= f(15, _, 1.2→float);
	Require: h's type[1] = string;
	Require: h("cheese") = "ok";

	i ::= f(15, …);
	Require: i's type[1] = string;
	Require: i's type[2] = float;
	Require: i("x", 2.5→float) = "ok";

	j ::= f(…, 1.7→float);
	Require: j's type[1] = integer;
	Require: j's type[2] = string;
	Require: j(45, "x") = "ok";
];
