/*
 * Delimited Iterators.avail
 * Copyright © 1993-2014, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

System Module "Delimited Iterators"
Versions
	"dev"
Uses
	"Abstract Iterators",
	"Collection Iterators" = ("an iterator over_"),
	"Foundation",
	"Unit Testing"
Names
	"delimited iterator test suite",
	"_delimited by_",
	/* TODO: [TLS] Implement this! */
	"_delimited by any of_"
Body

"delimiter|delimiters" is a new field atom;
"current value" is a new field atom;
"exhaustion state" is a new field atom;

/**
 * A delimited iterator answers tuples of elements from its decorated iterator.
 * These tuples are separated by occurrences of the delimiter.
 *
 * @type "delimited iterator"
 * @supertype "iterator"
 */
Class "delimited iterator" extends iterator
	with fields
		element type : tuple's type,
		decorated iterator : iterator,
		delimiter : <any…|1..>
	with mutable fields
		referable current value : tuple,
		exhaustion state : boolean;

Method "_is exhausted" is
[
	anIterator : delimited iterator
|
	anIterator's exhaustion state
] : boolean;

delimited iterator's current element method is
[
	anIterator : delimited iterator
|
	/* If this is the first inquiry, then advance the iterator (to capture the
	 * current element).
	 */
	If ↑anIterator's current value is unassigned then
	[
		Advance anIterator;
	];
	anIterator's current value
] : tuple;

delimited iterator's advance method is
[
	anIterator : delimited iterator
|
	inner ::= anIterator's decorated iterator;
	If inner is exhausted then
	[
		Clear ↑anIterator's current value;
		anIterator's exhaustion state := true;
	]
	else
	[
		result : tuple := <>;
		delimiter ::= anIterator's delimiter;
		index : natural number := 1;
		Until [inner is exhausted ∨ [index > |delimiter|]]
		do
		[
			value ::= inner's current element;
			If value = delimiter[index] then
			[
				↑index++;
			]
			else
			[
				result := eject ↑result ++ delimiter[1..index-1] ++ <value>;
				index := 1;
			];
			Advance inner;
		];
		anIterator's current value := result;
	];
] : ⊤;

/**
 * Construct and answer an {@type "iterator"} that produces tuples of values
 * from {@param "anIterator"}. These tuples are separated by occurrences of
 * {@param "delimiterTuple"} (which will never be produced as output from the
 * answered iterator).
 *
 * @method "_delimited by_"
 * @param "anIterator" "iterator"
 *        An iterator.
 * @param "delimiterTuple" "tuple"
 *        The delimiter.
 * @returns "iterator of tuple"
 *          The requested iterator.
 */
Public method "_delimited by_" is
[
	anIterator : iterator,
	delimiterTuple : <any…|1..>
|
	a delimited iterator with
		element type ::= <anIterator's element type…|>,
		decorated iterator ::= anIterator,
		delimiter ::= delimiterTuple,
		exhaustion state := false
] : iterator of tuple;

Semantic restriction "_delimited by_" is
[
	iteratorType : iterator's type,
	delimiterType : <any…|1..>'s type
|
	extend delimited iterator with
		element type : <iteratorType's element type's instance…|>'s type,
		decorated iterator : iteratorType,
		delimiter : delimiterType
];

"delimited iterator test suite" is a new atom;
New test suite delimited iterator test suite extends iterators test suite;

Test "delimited iterator basics" in delimited iterator test suite is
[
	ref ::= 1 to 10;
	d ::= <1, 0, 2, 0, 3, 0, 4, 0, 5, 0, 6, 0, 7, 0, 8, 0, 9, 0, 10>;
	index : natural number := 1;
	iter ::= an iterator over d delimited by <0>;
	For each iter do
	[
		element : <[0..10]…|>
	|
		Require: <ref[(↑index++)]> = element;
	];
	Require: (|ref| + 1) = index;
];

Test "delimited iterator(no occurrences)" in delimited iterator test suite is
[
	ref ::= 1 to 10;
	iter ::= an iterator over 1 to 10 delimited by <0>;
	element ::= iter's current element;
	Require: ref = element;
	Advance iter;
	Require: iter is exhausted;
];
