/**
 * 3-exception.avail
 * Copyright (c) 2010, Mark van Gulik.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "3-exception"
Extends
Uses
	"2-main",
	"3-object"
Names
	"Exception",
	"catch_in_",
	"Raise_",
	"result"
Body


/* Exception support.  The exception model is kept simple by only supporting
   terminating exceptions.  This means that when an exception is thrown via
   Raise_, the handler will be invoked in such a way that the Raise_ will not
   return.  It's multi-threaded (continuation chains hold all exception information). */


result ::= lookup "result";

"Exception" is explicit subtype of Object with [result->all];

Method "_result" is [ex : Exception |
	ex -> result;
]
returns [exType : Exception type |
	exType -> result;
];


Method "_with_" is [et : Exception type, res : all |
	et new with <<result, res>>;
] : Exception
returns [ett : Exception type type, resT : type |
	ett instance;
]
requires [ett : Exception type type, resT : type |
	resT <= ett instance -> result;
];


/* Helper primitive... */

Method "_evaluate with handler_" is [body : []->void, handler : [terminates]->void |
	/* Primitive 200 is a special primitive.  When the Raise_ primitive is
	   invoked, it scans the stack for a continuation whose closure's
	   code is primitive 200, and it invokes this continuation's second
	   argument, the handler block, with the argument passed to Raise_.
	   Aside from that, primitive 200 always fails, so the backup code
	   should just invoke the body. */
	Primitive 200;
	body();
]
returns [bt : closureType, ht : closureType |
	type union of bt result and ht result;
]
requires [bt : closureType, ht : closureType |
	/* We require the exception handler to accept some subtype of
	   Exception.  Only some handlers are applicable, therefore, based
	   on the Exception subtype, so a dynamic check determines which
	   handlers to skip over.  */
	ht [1] <= Exception;
];


Method "catch_in_" is [handler : [terminates]->void, body : []->void |
	body evaluate with handler handler;
]
returns [ht : closureType, bt : closureType |
	type union of ht result and bt result;
]
requires [ht : closureType, bt : closureType |
	/* We require the exception handler to accept some subtype of
	   Exception.  Only some handlers are applicable, therefore, based
	   on the Exception subtype, so a dynamic check determines which
	   handlers to skip over.  */
	ht [1] <= Exception;
];



/* Helper method... */

Forward "handleTuple_do_" for [tuple of [terminates]->void, []->void]->void;
Forward "handleTuple_do_" for [tuple of [terminates]->all, []->all]->all;

Method "handleTuple_do_" is [
		handlers : tuple of [terminates]->void,
		body : []->void |
	/* Treat the last one as innermost. */
	if handlers = <> then
		body
	else [
		[
			handleTuple handlers[2..||handlers||] do body;
		] evaluate with handler handlers[1];
	];
] : void;

Method "handleTuple_do_" is [
		handlers : tuple of [terminates]->all,
		body : []->all |
	/* Treat the last one as innermost.  This is an overide for the case
	   where the body and all handlers produce a result. */
	if handlers = <> then
		body
	else [
		[
			handleTuple handlers[2..||handlers||] do body;
		] evaluate with handler handlers[1];
	];
] : all;


Method "catch_in_" is [
		handlerList : tuple of [terminates]->void as listType,
		body : []->void |
	/* Attach multiple handlers simultaneously.  Treat the last
	   handler as the innermost one. */
	handleTuple <handlerList> do body;
] : void
returns [listT : listType, bodyT : closure type |
	merge : type := cast bodyT into [bt2 : closureType |
		bt2 result;
	] otherwise [
		void;
	];
	tt ::= listT as tupleType;
	merge := type union of merge and
		cast tt default type into [hndT : closureType |
			hndT;
		] otherwise [
			void;
		];
	tt leading types do [t : type |
		merge := type union of merge and
			cast t into [hndT : closureType |
				hndT;
			] otherwise [
				void;
			];
	];
	merge;
]
requires [listT : listType, bodyT : closure type |
	/* We require each exception handler to accept some subtype of
	   Exception.  Only some handlers are applicable, therefore, based
	   on the Exception subtype, so a dynamic check determines which
	   handlers to skip over.  */
	tt ::= listT as tupleType;
	union ::= type union of tt default type and
		union of types in tt leading types;
	cast union into [ut : closureType |
		ut [1] <= Exception;
	] otherwise [
		Crash "All the handler block types must be known statically.";
	];
];


Method "Raise_" is [exception : Exception |
	/* This primitive scans the stack for frames that are failed invocations
	   of primitive 200 (which always fails).  After finding one whose second
	   argument (a handler block of one argument) will accept the given
	   exception object, pop the stack to leave the primitive 200 method on
	   the top, and invoke the handler with the exception object. */
	Primitive 201;
	Crash "No suitable handler was found for the exception";
] : terminates;



