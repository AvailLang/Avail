/*
 * File Test Utilities.avail
 * Copyright © 1993-2020, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "File Test Utilities"
Versions
	"1.4.0"
Uses
	"Avail",
	"Common"
Names
	/* File names. */
	"test directory",
	"abracadabra.txt",
	"scratch.txt",

	/* Potentially unsupported operations. */
	"Try_",

	/* Utilities. */
	"Randomly permute file access rights for__times"
Body

Private method "a test directory" is
[
	temporary directory/“a UUID” (UUID)
];

/* The test directory. */
testDir : file name;

Before running I/O test suite, do
[
	testDir := a test directory;
	Create a directory named testDir;
	Using a file writer for testDir/"abracadabra.txt"
		with options {create new}, do
	[
		f : file writer
	|
		bytes ::= take at most ∞ from
			a reader over "abracadabra"
				converted by a UTF8 encoder;
		Write bytes to f;
	];
	Close a file writer for testDir/"scratch.txt"
		with options {create new};
];

After running I/O test suite, do
[
	Recursively unlink testDir if necessary;
	Clear testDir;
];

Method "test directory" is [testDir];

Public method "abracadabra.txt" is
[
	test directory/"abracadabra.txt"
];

Public method "scratch.txt" is
[
	test directory/"scratch.txt"
];

Public method "Try_" is
[
	aFunction : []→⊤
|
	Guard aFunction
	intercept [e : unsupported-operation exception | /* Ignore. */];
] : ⊤;

Public method "Randomly permute file access rights for__times" is
[
	f : [{file access right|0..9}]→⊤,
	count : whole number
|
	allRights ::= file access right's instances→tuple ?→ <file access right…|9>;
	rng ::= a pRNG;
	Repeat
	[
		n ::= rng's next [0..12];
		rights : {file access right|} := ∅;
		Repeat [rights := rights + rng's next element of allRights;] n times;
		/* Java attempts to *open* files with read only access in order to
		 * extract their permission bits.  Obviously that won't work if the
		 * owner read permission is absent (at least if group/other read are also
		 * absent).  Just add owner read to keep things happy.
		 */
		rights := rights + owner read;
		f(rights);
	] count times;
];
