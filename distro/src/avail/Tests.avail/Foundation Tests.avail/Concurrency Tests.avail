/*
 * Concurrency Tests.avail
 * Copyright © 1993-2022, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Concurrency Tests"
Versions
	"Avail-1.6.0"
Uses
	"Common"
Names
	"concurrency test suite"
Body

hx : non-reentrant mutex := a non-reentrant mutex named "hname mutex";
hn : natural number := 1;
Private method "next hname" is [lock hx for ["h" ++ “(hn++)”]];

"concurrency test suite" is a new atom;
New test suite, concurrency test suite, extends foundation test suite;

Test "reenter non-reentrant mutex" in concurrency test suite is
[
	mutex ::= a non-reentrant mutex;
	Lock mutex for [Lock mutex for [Raise an exception]]
]
must raise reentry exception;

Test "reenter reentrant mutex" in concurrency test suite is
[
	mutex ::= a reentrant mutex;
	Require: lock mutex for [lock mutex for [5]] = 5;
];

Test "basic fork and join" in concurrency test suite is
[
	forked ::= invoke [Ignore: 0;] with <>, forked at priority 50;
	Join forked;
	Require: forked has terminated;
];

Test "fork and join with result" in concurrency test suite is
[
	forked ::= invoke [10 + 14] with <>, forked at priority 50;
	Join forked;
	Require: forked has terminated;
	Require: forked has result;
	Require: forked's result = 24;
];

Test "fork and multiple join" in concurrency test suite is
[
	target ::= invoke [3^20] with <>, forked at priority 50;
	waiters ::= map each ignored in 1 to 10 through
		[
			invoke [Join target;] with <>, forked at priority 50
		];
	For each f of waiters do [Join f;];
	Require: map waiters→set + target
		through [f : fiber | f has terminated] = {true};
	Require: target has result;
	Require: target's result = 3^20;
];

Test "basic binary semaphore" in concurrency test suite is
[
	sem ::= a semaphore with 0 out of 1 permits;
	forked ::= invoke [Signal sem;] with <>, forked at priority 50;
	Await sem;
	Join forked;
	Require: forked has terminated;
];

Test "basic counting semaphore" in concurrency test suite is
[
	count ::= 10;
	sem ::= a semaphore with 0 out of count permits;
	fibers ::=
		map each ignored in 1 to 10 through
		[
			invoke [Signal sem;] with <>, forked at priority 50
		];
	Await sem count times;
	For each forked of fibers do
	[
		Join forked;
		Require: forked has terminated;
	];
];

Test "signal too many times" in concurrency test suite is
[
	sem ::= a semaphore with 0 out of 0 permits;
	Signal sem;
]
must raise cannot-exceed-maximum-permits exception;

Test "heritable fiber variables" in concurrency test suite is
[
	h1 ::= new heritable atom named next hname;
	h2 ::= new heritable atom named next hname;
	u1 ::= new atom named "u1";
	u2 ::= new atom named "u2";
	fiberLocals ::=
	{
		h1 → 100,
		h2 → {"United States" → "Washington, D.C."},
		u1 → ∅,
		u2 → "golem"
	};
	For each key → value of fiberLocals do [current fiber[key] := value;];
	inherited : {atom→any|} := {};
	forked ::=
		invoke
		[
			For each key of fiberLocals' keys do
			[
				If key ∈ current fiber then
				[
					inherited := inherited + key→current fiber[key];
				];
			];
		]
		with <>,
		forked at priority 50;
	Join forked;
	Require: forked has terminated;
	Require: ¬forked has result;
	Require: h1 ∈ inherited;
	Require: inherited[h1] = fiberLocals[h1];
	Require: h2 ∈ inherited;
	Require: inherited[h2] = fiberLocals[h2];
	Require: ¬u1 ∈ inherited;
	Require: ¬u2 ∈ inherited;
];

Test "heritable fiber variables (orphan)" in concurrency test suite is
[
	h1 ::= new heritable atom named next hname;
	h2 ::= new heritable atom named next hname;
	u1 ::= new atom named "u1";
	u2 ::= new atom named "u2";
	fiberLocals ::=
	{
		h1 → ∅,
		h2 → "golem",
		u1 → 100,
		u2 → {"United States" → "Washington, D.C."}
	};
	For each key → value of fiberLocals do [current fiber[key] := value;];
	mutex ::= a monitor named "heritable fiber variables";
	isDone : boolean := false;
	done ::= a condition of mutex such that [isDone];
	inherited : {atom→any|} := {};
	Invoke
	[
		For each key of fiberLocals' keys do
		[
			If key ∈ current fiber then
			[
				inherited := inherited + key→current fiber[key];
			];
		];
		Lock mutex for
		[
			isDone := true;
			Signal done;
		];
	]
	with <>,
	forked at priority 50;
	Lock mutex for [Await done;];
	Require: h1 ∈ inherited;
	Require: inherited[h1] = fiberLocals[h1];
	Require: h2 ∈ inherited;
	Require: inherited[h2] = fiberLocals[h2];
	Require: ¬u1 ∈ inherited;
	Require: ¬u2 ∈ inherited;
];

Test "sleep" in concurrency test suite is
[
	delay ::= 10;
	then ::= milliseconds since the Epoch;
	Sleep for delay milliseconds;
	now ::= milliseconds since the Epoch;
	Require: now - then ≥ delay;
];

Test "delayed fork" in concurrency test suite is
[
	delay ::= 10;
	then ::= milliseconds since the Epoch;
	now : integer;
	forked ::=
		in delay milliseconds,
			invoke [now := milliseconds since the Epoch;]
			with <>,
			forked at priority 50;
	Join forked;
	Require: forked has terminated ("1");
	Require: ¬forked has result ("2");
	Require: now is assigned ("3");
	Require: now - then ≥ delay ("4");
];

Test "delayed fork (orphan)" in concurrency test suite is
[
	delay ::= 10;
	then ::= milliseconds since the Epoch;
	now : integer;
	In delay milliseconds,
		invoke [now := milliseconds since the Epoch;]
		with <>,
		forked at priority 50;
	While [now is unassigned] do [Yield current fiber;];
	Require: now - then ≥ delay;
];

Test "heritable fiber variables (delayed fork)" in concurrency test suite is
[
	h1 ::= new heritable atom named next hname;
	h2 ::= new heritable atom named next hname;
	u1 ::= new atom named "u1";
	u2 ::= new atom named "u2";
	fiberLocals ::=
	{
		h1 → 100,
		h2 → {"United States" → "Washington, D.C."},
		u1 → ∅,
		u2 → "golem"
	};
	For each key → value of fiberLocals do [current fiber[key] := value;];
	inherited : {atom→any|} := {};
	delay ::= 10;
	then ::= milliseconds since the Epoch;
	now : integer;
	forked ::=
		in delay milliseconds,
		invoke
		[
			now := milliseconds since the Epoch;
			For each key of fiberLocals' keys do
			[
				If key ∈ current fiber then
				[
					inherited := inherited + key→current fiber[key];
				];
			];
		]
		with <>,
		forked at priority 50;
	Join forked;
	Require: forked has terminated;
	Require: ¬forked has result;
	Require: now - then ≥ delay;
	Require: h1 ∈ inherited;
	Require: inherited[h1] = fiberLocals[h1];
	Require: h2 ∈ inherited;
	Require: inherited[h2] = fiberLocals[h2];
	Require: ¬u1 ∈ inherited;
	Require: ¬u2 ∈ inherited;
];

Test "heritable fiber variables (delayed fork, orphan)"
	in concurrency test suite is
[
	h1 ::= new heritable atom named next hname;
	h2 ::= new heritable atom named next hname;
	u1 ::= new atom named "u1";
	u2 ::= new atom named "u2";
	fiberLocals ::=
	{
		h1 → ∅,
		h2 → "golem",
		u1 → 100,
		u2 → {"United States" → "Washington, D.C."}
	};
	For each key → value of fiberLocals do [current fiber[key] := value;];
	mutex ::= a monitor named "heritable fiber variables";
	isDone : boolean := false;
	done ::= a condition of mutex such that [isDone];
	inherited : {atom→any|} := {};
	delay ::= 10;
	then ::= milliseconds since the Epoch;
	now : integer;
	In delay milliseconds,
		invoke
		[
			now := milliseconds since the Epoch;
			For each key of fiberLocals' keys do
			[
				If key ∈ current fiber then
				[
					inherited := inherited + key→current fiber[key];
				];
			];
			Lock mutex for
			[
				isDone := true;
				Signal done;
			];
		]
		with <>,
		forked at priority 50;
	Lock mutex for [Await done;];
	Require: now - then ≥ delay;
	Require: h1 ∈ inherited;
	Require: inherited[h1] = fiberLocals[h1];
	Require: h2 ∈ inherited;
	Require: inherited[h2] = fiberLocals[h2];
	Require: ¬u1 ∈ inherited;
	Require: ¬u2 ∈ inherited;
];

Test "basic future" in concurrency test suite is
[
	f : future of whole number := the future of [10^100];
	Require: f's value = 10^100;
];

Test "future raises" in concurrency test suite is
[
	f : future of whole number := the future of
	[
		u : whole number := 1;
		v : whole number := 0;
		u ÷ v
	];
	killer : worker exception;
	Guard [Ignore: f's value;]
	intercept [e : worker exception | killer := e;];
	Require: killer is assigned;
];

Test "for-each in parallel" in concurrency test suite is
[
	count : whole number := 0;
	mutex ::= a non-reentrant mutex named "for-each lock";
	For each of 1 to 10 in parallel do
	[
		i : [1..10]
	|
		Lock mutex for [count := count + i;];
	];
	Require: count = ∑ 1 to 10;
];

Test "macro for-each in parallel" in concurrency test suite is
[
	count : whole number := 0;
	mutex ::= a non-reentrant mutex named "for-each lock";
	For each i of 1 to 10 in parallel do
	[
		Lock mutex for [count := count + i;];
	];
	Require: count = ∑ 1 to 10;
];

Test "macro for-each in parallel (set)" in concurrency test suite is
[
	count : whole number := 0;
	mutex ::= a non-reentrant mutex named "for-each lock";
	aSet ::= 1 to 10 → set;
	For each i of aSet in parallel do
	[
		Lock mutex for [count := count + i;];
	];
	Require: count = ∑ 1 to 10;
];

Test "macro for-each in parallel tuple index" in concurrency test suite is
[
	count : whole number := 0;
	mutex ::= a non-reentrant mutex named "for-each lock";
	For each n,i of 1 to 10 in parallel do
	[
		Lock mutex for [count := count + i + n;];
	];
	Require: count = 2 × ∑ 1 to 10;
];

Test "macro for-each in parallel (map)" in concurrency test suite is
[
	count : whole number := 0;
	mutex ::= a non-reentrant mutex named "for-each lock";
	aMap : {[1..10]→[1..10]|} := {};
	For each i of 1 to 10 do [aMap := aMap + i → i;];
	For each n→i of aMap in parallel do
	[
		Lock mutex for [count := count + i + n;];
	];
	Require: count = 2 × ∑ 1 to 10;
];

Test "for-each in parallel (small window)" in concurrency test suite is
[
	ref ::= "abcdefghijklmnopqrstuvwxyz";
	s : string := "";
	/* Test the windowing mechanism by making the loop completely serial, so
	 * that we don't need a mutex.
	 */
	For each of 1 to |ref| in parallel 1-way do
	[
		i : natural number
	|
		s ++= ref[i..i];
	];
	Require: s = ref;
];

Test "macro for-each in parallel (small window)" in concurrency test suite is
[
	ref ::= "abcdefghijklmnopqrstuvwxyz";
	s : string := "";
	/* Test the windowing mechanism by making the loop completely serial, so
	 * that we don't need a mutex.
	 */
	For each i of 1 to |ref| in parallel 1-way do [s ++= ref[i..i];];

	s2 : string := "";
	For each n,i of 1 to |ref| in parallel 1-way do [s2 ++= ref[i..n];];
	Require: s = ref;
];

Test "for-each in parallel (worker exception)" in concurrency test suite is
[
	killer : worker exception;
	Guard
	[
		For each of 1 to 10 in parallel do
		[
			i : [1..10]
		|
			Ignore: i ÷ (i - 1);
		];
	]
	intercept
	[
		e : worker exception
	|
		killer := e;
	];
	Require: killer is assigned;
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "map in parallel (tuple)" in concurrency test suite is
[
	result ::= map 1 to 10 in parallel through [i : [1..10] | i × 5];
	expected ::= map each i in 1 to 10 through [i × 5];
	Require: result = expected;
];

Test "macro map in parallel (tuple)" in concurrency test suite is
[
	result ::= map each i of 1 to 10 in parallel through [i × 5];
	expected ::= map each i in 1 to 10 through [i × 5];
	Require: result = expected;
];

Test "map in parallel (tuple, small window)" in concurrency test suite is
[
	result ::= map 1 to 10 in parallel 2-way through [i : [1..10] | i × 5];
	expected ::= map each i in 1 to 10 through [i × 5];
	Require: result = expected;
];

Test "macro map in parallel (tuple, small window)" in concurrency test suite is
[
	result ::= map each i of 1 to 10 in parallel 2-way through [i × 5];
	expected ::= map each i in 1 to 10 through [i × 5];
	Require: result = expected;
];

Test "map in parallel (tuple, worker exception)" in concurrency test suite is
[
	result ::=
		guard [map 1 to 10 in parallel through [i : [1..10] | i ÷ (i - 1)]]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "map in parallel (set)" in concurrency test suite is
[
	result ::= map 1 to 10→set in parallel through [i : [1..10] | i × 5];
	expected ::= map each i in 1 to 10→set through [i × 5];
	Require: result = expected;
];

Test "macro map in parallel (set)" in concurrency test suite is
[
	result ::= map each i of 1 to 10→set in parallel through [i × 5];
	expected ::= map each i in 1 to 10→set through [i × 5];
	Require: result = expected;
];

Test "map in parallel (set, small window)" in concurrency test suite is
[
	result ::= map 1 to 10→set in parallel 3-way through [i : [1..10] | i × 5];
	expected ::= map each i in 1 to 10→set through [i × 5];
	Require: result = expected;
];

Test "macro map in parallel (set, small window)" in concurrency test suite is
[
	result ::= map each i of 1 to 10→set in parallel 3-way through [i × 5];
	expected ::= map each i in 1 to 10→set through [i × 5];
	Require: result = expected;
];

Test "map in parallel (set, worker exception)" in concurrency test suite is
[
	result ::=
		guard [map 1 to 10→set in parallel through [i : [1..10] | i ÷ (i - 1)]]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "map in parallel (map)" in concurrency test suite is
[
	m ::= 1 to 10 zip map 1 to 10 through [n : [1..10] | n × n]→map;
	result ::= map m in parallel through [k : [1..10], v : [1..100] | k × v];
	expected ::= map each k→v in m through [k × v];
	Require: result = expected;
];

Test "macro map in parallel (map)" in concurrency test suite is
[
	m ::= 1 to 10 zip map 1 to 10 through [n : [1..10] | n × n]→map;
	result ::= map each k→v of m in parallel through [k × v];
	expected ::= map each k→v in m through [k × v];
	Require: result = expected;
];

Test "map in parallel (map, small window)" in concurrency test suite is
[
	m ::= 1 to 10 zip map each n in 1 to 10 through [n × n]→map;
	result ::= map m in parallel 5-way
		through [k : [1..10], v : [1..100] | k × v];
	expected ::= map each k→v in m through [k × v];
	Require: result = expected;
];

Test "macro map in parallel (map, small window)" in concurrency test suite is
[
	m ::= 1 to 10 zip map each n in 1 to 10 through [n × n]→map;
	result ::= map each k→v of m in parallel 5-way through [k × v];
	expected ::= map each k→v in m through [k × v];
	Require: result = expected;
];

Test "map in parallel (map, worker exception)" in concurrency test suite is
[
	m ::= 1 to 10 zip 0 to 9→map;
	result ::=
		guard [map m in parallel through [i : [1..10], j : [0..9] | i ÷ j]]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "filter in parallel (tuple)" in concurrency test suite is
[
	result ::= filter 1 to 10 in parallel by [i : [1..10] | i is odd];
	expected ::= filter 1 to 10 by [i : [1..10] | i is odd];
	Require: result = expected;
];

Test "select in parallel (tuple macro)" in concurrency test suite is
[

	result ::= select each i from 1 to 10 in parallel where [i is odd];
	expected ::= select each i from 1 to 10 where [i is odd];
	Require: result = expected;
];

Test "reject in parallel (tuple macro)" in concurrency test suite is
[

	result ::= reject each i from 1 to 10 in parallel where [i is odd];
	expected ::= reject each i from 1 to 10 where [i is odd];
	Require: result = expected;
];

Test "select in parallel (tuple macro 2 input)" in concurrency test suite is
[

	result ::= select each i, j from 1 to 10 in parallel
		where [i is odd  ∧ ¬j is even];
	expected ::= select each i, j from 1 to 10
		where [i is odd  ∧ ¬j is even];
	Require: result = expected;
];

Test "reject in parallel (tuple macro 2 input)" in concurrency test suite is
[

	result ::= reject each i, j from 1 to 10 in parallel
		where [i is odd  ∧ ¬j is even];
	expected ::= reject each i, j from 1 to 10
		where [i is odd  ∧ ¬j is even];
	Require: result = expected;
];

Test "filter in parallel (tuple, small window)" in concurrency test suite is
[
	result ::= filter 1 to 10 in parallel by [i : [1..10] | i is odd];
	expected ::= filter 1 to 10 in parallel by [i : [1..10] | i is odd];
	Require: result = expected;
];

Test "filter in parallel (tuple, worker exception)" in concurrency test suite is
[
	result ::=
		guard
		[
			filter 1 to 10 in parallel by [i : [1..10] | i ÷ (i - 1) is odd]
		]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "filter in parallel (set)" in concurrency test suite is
[
	result ::= filter 1 to 10→set in parallel by [i : [1..10] | i is odd];
	expected ::= filter 1 to 10→set by [i : [1..10] | i is odd];
	Require: result = expected;
];

Test "select in parallel (set macro)" in concurrency test suite is
[

	result ::= select each i from 1 to 10→set in parallel where [i is odd];
	expected ::= select each i from 1 to 10→set where [i is odd];
	Require: result = expected;
];

Test "reject in parallel (set macro)" in concurrency test suite is
[

	result ::= reject each i from {1,2,3,4,5,6,7,8,9,10} in parallel where [i is odd];
	expected ::= reject each i from 1 to 10→set where [i is odd];
	Require: result = expected;
];

Test "filter in parallel (set, small window)" in concurrency test suite is
[
	result ::= filter 1 to 10→set in parallel by [i : [1..10] | i is odd];
	expected ::= filter 1 to 10→set in parallel by [i : [1..10] | i is odd];
	Require: result = expected;
];

Test "filter in parallel (set, worker exception)" in concurrency test suite is
[
	result ::=
		guard
		[
			filter 1 to 10→set in parallel by [i : [1..10] | i ÷ (i - 1) is odd]
		]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "filter in parallel (map)" in concurrency test suite is
[
	m ::= 1 to 10 zip map each i in 1 to 10 through [i is odd]→map;
	result ::= filter m in parallel by [k : [1..10], v : boolean | v];
	expected ::= filter m by [k : [1..10], v : boolean | v];
	Require: result = expected;
];

Test "select in parallel (map macro)" in concurrency test suite is
[
	m ::= 1 to 10 zip map each i in 1 to 10 through [i is odd]→map;
	result ::= select each k→v from m in parallel where [v];
	expected ::= select each k→v from m where [v];
	Require: result = expected;
];

Test "reject in parallel (map macro)" in concurrency test suite is
[
	m ::= 1 to 10 zip map each i in 1 to 10 through [i is odd]→map;
	result ::= reject each k→v from m in parallel where [v];
	expected ::= reject each k→v from m where [v];
	Require: result = expected;
];

Test "filter in parallel (map, small window)" in concurrency test suite is
[
	m ::= 1 to 10 zip map each i in 1 to 10 through [i is odd]→map;
	result ::= filter m in parallel 3-way by [k : [1..10], v : boolean | v];
	expected ::= filter m by [k : [1..10], v : boolean | v];
	Require: result = expected;
];

Test "filter in parallel (map, worker exception)" in concurrency test suite is
[
	m ::= 1 to 10 zip 10 of [i : [0..10] | i is odd]→map;
	result ::=
		guard
		[
			filter m in parallel
				by [k : [1..10], v : [[0..10]]→boolean | v(k ÷ (k - 1))]
		]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "map and filter in parallel (tuple)" in concurrency test suite is
[
	f ::= [i : [1..10], k : [[2..20]]→⊤ | If i is even then [k(2 × i);];];
	result ::= map and filter 1 to 10 in parallel by f;
	expected ::= map and filter 1 to 10 by f;
	Require: result = expected;
];

Test "map and filter in parallel (tuple, small window)"
	in concurrency test suite is
[
	f ::= [i : [1..10], k : [[2..20]]→⊤ | If i is even then [k(2 × i);];];
	result ::= map and filter 1 to 10 in parallel 3-way by f;
	expected ::= map and filter 1 to 10 by f;
	Require: result = expected;
];

Test "map and filter in parallel (tuple, worker exception)"
	in concurrency test suite is
[
	f ::= [i : [1..10], k : [[11..100]]→⊤ | If i is odd then [k(100÷(i-1));];];
	result ::=
		guard [map and filter 1 to 10 in parallel by f]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "map and filter in parallel (set)" in concurrency test suite is
[
	f ::= [i : [1..10], k : [[2..20]]→⊤ | If i is even then [k(2 × i);];];
	result ::= map and filter 1 to 10→set in parallel by f;
	expected ::= map and filter 1 to 10→set by f;
	Require: result = expected;
];

Test "map and filter in parallel (set, small window)"
	in concurrency test suite is
[
	f ::= [i : [1..10], k : [[2..20]]→⊤ | If i is even then [k(2 × i);];];
	result ::= map and filter 1 to 10→set in parallel 2-way by f;
	expected ::= map and filter 1 to 10→set by f;
	Require: result = expected;
];

Test "map and filter in parallel (set, worker exception)"
	in concurrency test suite is
[
	f ::= [i : [1..10], k : [[11..100]]→⊤ | If i is odd then [k(100÷(i-1));];];
	result ::=
		guard [map and filter 1 to 10→set in parallel by f]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "map and filter in parallel (map)" in concurrency test suite is
[
	f ::=
		[
			k : [1..10],
			v : [1..100],
			s : [[2..200]]→⊤
		|
			If k is odd then [s(2 × v);];
		];
	m ::= 1 to 10 zip map each i in 1 to 10 through [i × i]→map;
	result ::= map and filter m in parallel by f;
	expected ::= map and filter m by f;
	Require: result = expected;
];

Test "map and filter in parallel (map, small window)"
	in concurrency test suite is
[
	f ::=
	[
		k : [1..10],
		v : [1..100],
		s : [[2..200]]→⊤
	|
		If k is odd then [s(2 × v);];
	];
	m ::= 1 to 10 zip map each i in 1 to 10 through [i × i]→map;
	result ::= map and filter m in parallel 4-way by f;
	expected ::= map and filter m by f;
	Require: result = expected;
];

Test "map and filter in parallel (map, worker exception)"
	in concurrency test suite is
[
	f ::=
		[
			k : [1..10],
			v : [1..100],
			s : [[0..100]]→⊤
		|
			If k is odd then [s(v÷(k-1));];
		];
	m ::= 1 to 10 zip map each i in 1 to 10 through [i × i]→map;
	result ::=
		guard [map and filter m in parallel by f]
		intercept [e : worker exception | e];
	killer ::=
		cast result into [e : worker exception | e]
		else [Fail test: format "unexpected value: “①”" with result];
	Require: killer's causal exception ∈ cannot-divide-by-zero exception;
];

Test "request termination of running fiber" in concurrency test suite is
[
	/* Try it a few times so that races can resolve differently. */
	Repeat
	[
		forked ::=
			invoke
			[
				Until termination requested do [Yield current fiber;];
				"仕上がります"
			]
			with <>,
			forked at priority 50;
		Request termination of forked;
		Join forked;
		Require: forked has terminated;
		Require: forked has result;
		Require: forked's result = "仕上がります";
	] 10 times;
];

Test "request termination of parked fiber" in concurrency test suite is
[
	/* Try it a few times so that races can resolve differently. */
	Repeat
	[
		forked ::=
			invoke [Do [Park current fiber;] until termination requested;]
			with <>,
			forked at priority 50;
		Request termination of forked;
		Join forked;
		Require: forked has terminated;
	] 10 times;
];

Test "request termination of joining fiber" in concurrency test suite is
[
	/* Try it a few times so that races can resolve differently. */
	Repeat
	[
		forked ::=
			invoke [Do [Park current fiber;] until termination requested;]
			with <>,
			forked at priority 50;
		joiner ::=
			invoke [Join forked, then honor a termination request;]
			with <>,
			forked at priority 50;
		Request termination of joiner;
		Join joiner;
		Require: joiner has terminated;
		Require: ¬forked has terminated;
		Request termination of forked;
		Join forked;
		Require: forked has terminated;
	] 10 times;
];

Test "request termination of sleeping fiber" in concurrency test suite is
[
	/* Try it a few times so that races can resolve differently. */
	Repeat
	[
		forked ::=
			invoke
			[
				Do [Sleep for 1000000 milliseconds;]
				until termination requested;
			]
			with <>,
			forked at priority 50;
		Request termination of forked;
		Join forked;
		Require: forked has terminated;
	] 10 times;
];

Test "request termination of permanently sleeping fiber"
	in concurrency test suite is
[
	/* Try it a few times so that races can resolve differently. */
	Repeat
	[
		forked ::=
			invoke
				[Do [Sleep for ∞ milliseconds;] until termination requested;]
			with <>,
			forked at priority 50;
		Request termination of forked;
		Join forked;
		Require: forked has terminated;
	] 10 times;
];
