/*
 * Backtracking Tests.avail
 * Copyright © 1993-2017, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Backtracking Tests"
Versions
	"1.0.0 DEV 2014-04-28"
Uses
	"Common"
Names
	"backtracking test suite"
Body

"backtracking test suite" is a new atom;
New test suite backtracking test suite extends foundation test suite;

Test "Cartesian product" in backtracking test suite is
[
	ref ::= (5 of true ++ 5 of false) zip (1 to 5 ++ 1 to 5);
	product ::= all values of [<first try, trial# of 5>];
	Require: product = ref;
];

Test "partial Cartesian product" in backtracking test suite is
[
	ref ::= (5 of true ++ 5 of false) zip (1 to 5 ++ 1 to 5);
	For each i from 0 to |ref| do
	[
		product ::= first i values of
			[<trial element of <true, false>, trial# of 5>];
		Require: product = ref[1..i];
	];
];

Private method "_queens" is
[
	n : natural number
|
	queens : <natural number…|> := 1 to n;
	Backtracking fence
	[
		For each i from 1 to n do
		[
			queens := eject queens[i] → trial# of n;
			For each j from 1 to i - 1 do
			[
				If queens[i] = queens[j] then [Try again];
				If |queens[i] - queens[j]| = i - j then [Try again];
			];
		];
	];
	queens
] : <natural number…|>;

Semantic restriction "_queens" is
[
	n : natural number's type
|
	<<>, [1..⎡n⎤]…|n>
];

Test "eight queens" in backtracking test suite is
[
	solution : <[1..8]…|8> := 8 queens;
	Require: solution = <1, 5, 8, 6, 3, 7, 2, 4>;
];
