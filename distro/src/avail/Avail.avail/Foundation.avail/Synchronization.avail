/*
 * Synchronization.avail
 * Copyright © 1993-2021, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Synchronization"
Versions
	"Avail-1.6.0"
Uses
	"Assertions",
	"Atoms",
	"Bootstrap",
	"Control Structures",
	"Definers",
	"Early Conditionals",
	"Enumeration Support",
	"Exceptions",
	"Format",
	"Functions",
	"Literals",
	"Logic",
	"Math",
	"Objects",
	"Tuples",
	"Types",
	"Strings",
	"Variables"
Names
	/* Semaphores. */
	"Await__time|times",
	"Signal__time|times",
	"binary semaphore",
	"counting semaphore",
	"semaphore",
	"_'s⁇maximum permits",

	/* Semaphore construction. */
	"a semaphore«named_»with_«out of_»permit|permits",

	/* Mutexes. */
	"monitor",
	"non-reentrant mutex",
	"reentrant mutex",
	"_'s⁇owner's name",

	/* Mutex construction. */
	"a monitor«named_»",
	"a non-reentrant mutex«named_»",
	"a reentrant mutex«named_»",

	/* Conditions. */
	"Await_",
	"Signal_",
	"Signal_if satisfied",
	"a condition of_such that_",
	"condition",
	"_is satisfied",

	/* Structured synchronization. */
	"Lock|lock_for_",

	/* Exceptions. */
	"condition exception",
	"cannot-await-condition exception",
	"cannot-exceed-maximum-permits exception",
	"cannot-signal-condition exception",
	"condition-unsatisfied exception",
	"reentry exception",
	"semaphore exception",
	"synchronization exception",
	"_'s⁇misused condition",
	"_'s⁇misused semaphore",
	"_'s⁇reentered mutex",
	"_'s⁇reentering fiber",

	/* Low-level synchronization. */
	"Atomically update_using_",
	"Atomically update_↑using_",
	"Join_",
	"Join_,then honor a termination request",
	"Park current fiber,then honor a termination request",
	"dequeue_from_",
	"parking queue",
	"priority enqueue_on_",
	"synchronization device"
Body

Semantic restriction "atomically read_↑and store_" is
[
	varType : variable's type,
	valueType : any's type
|
	If ¬valueType ⊆ varType's write type then
	[
		Reject parse, expected:
			format
				"value's type “①” to be a subtype of variable's write type “②”"
			with valueType, varType's write type
	];
	⊤
];

Semantic restriction "atomically compare_↑to_and,⁇if equal,⁇store_" is
[
	varType : variable's type,
	referenceType : any's type,
	valueType : any's type
|
	If ¬referenceType ⊆ varType's read type then
	[
		Reject parse, expected:
			format
				"reference value's type “①” to be a subtype of variable's \
				\|read type “②”"
			with referenceType, varType's read type
	];
	If ¬valueType ⊆ varType's write type then
	[
		Reject parse, expected:
			format
				"replacement value's type “①” to be a subtype of variable's \
				\|write type “②”"
			with valueType, varType's write type
	];
	⊤
];

/**
 * Atomically update the specified variable to a value derived from its current
 * value and the given function.
 *
 * @method "Atomically update_↑using_"
 * @param "var" "variable"
 * @param "f" "[any]→any"
 * @returns "⊤"
 * @category "Synchronization" "Variables" "Mutators" "Queries"
 */
Public method "Atomically update_↑using_" is
[
	var : variable,
	f : [⊥]→any
|
	Until
	[
		basis ::= ↓var;
		value ::= f(basis);
		unsafely atomically compare var to basis and, if equal, store value
	];
] : ⊤;

Alias "Atomically update_using_" to "Atomically update_↑using_";

Semantic restriction "Atomically update_↑using_" is
[
	var : variable's type,
	f : ([⊥]→any)'s type
|
	If ¬var's read type ⊆ f[1] then
	[
		Reject parse, expected:
			format
				"variable's read type (“①”) to be a subtype of the \
				\|function's parameter type (“②”)"
			with var's read type, f[1]
	];
	If ¬f's return type ⊆ var's write type then
	[
		Reject parse, expected:
			format
				"function's return type (“①”) to be a subtype of the \
				\|variable's write type (“②”)"
			with f's return type, var's write type
	];
	⊤
];

/**
 * The type of a {@type "tuple"} of {@type "fiber"}s.
 *
 * @type "parking queue"
 * @supertype "fiber*"
 * @category "Synchronization" "Types" "Constants"
 */
Public method "parking queue" is [fiber*];

/**
 * Queue the specified {@type "fiber"} on the {@type "parking queue"}, spinning
 * until it is possible to enqueue the fiber safely. Fibers are ordered first by
 * {@method "_'⁇s priority" priority} then seniority on the queue.
 *
 * @method "priority enqueue_on_"
 * @param "aFiber" "fiber"
 *        The {@type "fiber"} to enqueue for possible parking.
 * @param "queue" "parking queue"
 *        A {@type "parking queue"}.
 * @returns "boolean"
 *         {@method "true"} if the queue was empty at the time of enqueuing,
 *         {@method "false"} otherwise.
 * @category "Synchronization"
 */
Public method "priority enqueue_on_" is
[
	aFiber : fiber,
	queue : ↑parking queue
|
	lockAcquired : boolean;
	/* Spin on compare-and-swap until the parking queue can be updated.  Order
	 * the fibers by descending priority, then ascending seniority on the
	 * parking queue; the latter is accomplished by using ≥ instead of >.
	 */
	Until
	[
		basis ::= ↓queue;
		index ::= insertion index of aFiber in basis
			with [a : fiber, b : fiber | a's priority ≥ b's priority];
		withFiber ::= basis[index..index - 1] → <aFiber>;
		swapped ::= atomically compare queue to basis and,
			if equal, store withFiber;
		If swapped then [lockAcquired := basis is empty;];
		swapped
	];
	lockAcquired
] : boolean;

/**
 * Destructively dequeue the specified {@type "fiber"} from the {@type
 * "parking queue"}, spinning until it is possible to remove the fiber safely.
 *
 * @method "Dequeue_from_"
 * @param "aFiber" "fiber"
 *        A fiber.
 * @param "queue" "↑parking queue"
 *        A parking queue.
 * @returns "boolean"
 *         {@method "true"} if the queue was empty after the removal, {@method
 *         "false"} otherwise.
 * @category "Synchronization"
 */
Public method "dequeue_from_" is
[
	aFiber : fiber,
	queue : ↑parking queue
|
	lockReleased : boolean;
	Until
	[
		basis ::= ↓queue;
		index ::= first index of basis where [f : fiber | f = aFiber];
		sans ::= cast index into
		[
			i : natural number
		|
			basis[i..i] → <>
		]
		else
		[
			basis
		];
		swapped ::= atomically compare queue to basis and,
			if equal, store sans;
		If swapped then [lockReleased := sans is empty;];
		swapped
	];
	lockReleased
] : boolean;

/**
 * Park the {@method "current fiber"}. When the fiber awakens, honor an
 * outstanding {@method "termination requested" termination request}. If
 * termination has not been requested, then return control to the caller.
 *
 * @method "Park current fiber,then honor a termination request"
 * @returns "⊤"
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Public method "Park current fiber,then honor a termination request" is
[
	Park current fiber;
	Honor a termination request;
] : ⊤;

/**
 * Join the {@method "current fiber"} to the specified {@type "fiber"}. Ignore
 * {@method "termination requested" termination requests}. Once the specified
 * fiber {@method "_has terminated" has terminated}, then return control to the
 * caller.
 *
 * @method "Join_"
 * @param "joinee" "fiber"
 *        The fiber to join.
 * @returns "⊤"
 * @category "Synchronization"
 */
Public method "Join_" is
[
	joinee : fiber
|
	Until joinee has terminated do [Attempt to join joinee;];
];

/**
 * Join the {@method "current fiber"} to the specified {@type "fiber"}. If the
 * join is interrupted by a {@method "termination requested" termination
 * request}, then honor it. If termination has not been requested, then return
 * control to the caller.
 *
 * @method "Join_,then honor a termination request"
 * @param "joinee" "fiber"
 *        The fiber to join.
 * @returns "⊤"
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Public method "Join_,then honor a termination request" is
[
	joinee : fiber
|
	Until joinee has terminated do
	[
		Attempt to join joinee;
		Honor a termination request;
	];
];

"mutex name" is a new field atom;
"owner" is a new field atom;
"parked fibers" is a new field atom;

/**
 * A synchronization device controls concurrent access to protected resources.
 *
 * @type "synchronization device"
 * @supertype "object"
 * @category "Synchronization" "Data Abstractions" "Types"
 */
Public explicit class "synchronization device" extends object;

/**
 * A non-reentrant mutex is a mutual exclusion device that permits entry, but
 * not reentry, by only one {@type "fiber"}.
 *
 * @type "non-reentrant mutex"
 * @supertype "object"
 * @category "Synchronization" "Data Abstractions" "Types"
 */
Public class "non-reentrant mutex" extends synchronization device
	with fields
		mutex name : nonempty string ("_'s⁇name")
	with mutable fields
		owner : any,
		referable parked fibers : parking queue;

/**
 * Answer the {@type "non-reentrant mutex" mutex}'s {@type "fiber" owner}'s
 * name, or "«unnamed»" if unowned.
 *
 * @method "_'⁇s owner's name"
 * @param "mutex" "non-reentrant mutex"
 * @returns "string"
 * @category "Synchronization" "Queries"
 */
Public method "_'s⁇owner's name" is
[
	mutex : non-reentrant mutex
|
	cast mutex's owner
		into [f : fiber | f's name]
		else ["«unowned»"]
] : string;

/**
 * Always give preference to the specialized method "_'s⁇owner's name".
 *
 * @method "_'s⁇name"
 * @forbids "1" "_'s⁇owner"
 */
Grammatical restriction "_'s⁇name" is <{"_'s⁇owner"}>;

Method "“_”" is
[
	mutex : non-reentrant mutex
|
	format "‘typeDesc’ (‘myName’) [owner=‘owner’; parked=“parked”]" with
		typeDesc ::=
			(a|an) ““mutex's type's names” (as list partitioned by " ∩ ")”,
		myName ::= mutex's name,
		owner ::= mutex's owner's name,
		parked ::= map each f in mutex's parked fibers through [f's name]
];

/* The default name of a synchronization device, if no name is specified at
 * construction time.
 */
defaultName ::= "«unnamed»";

/**
 * Construct and answer a {@type "non-reentrant mutex"}.
 *
 * @method "a non-reentrant mutex«named_»"
 * @param "optionalName" "nonempty string?"
 *        If specified, then the name of the mutex, for improved diagnostics.
 *        If not specified, then {@code "«unnamed»"}.
 * @returns "non-reentrant mutex"
 * @category "Synchronization" "Data Abstractions" "Constructors"
 */
Public method "a non-reentrant mutex«named_»" is
[
	optionalName : nonempty string?
|
	myName ::= optionalName[1] else [defaultName];
	/* The ownership memento 0 is used to indicate that a mutex is unowned.
	 * It is non-canonical.
	 */
	a non-reentrant mutex with
		mutex name ::= myName,
		owner := 0,
		parked fibers := <>
] : non-reentrant mutex;

Semantic restriction "a non-reentrant mutex«named_»" is
[
	optionalName : nonempty string?'s type
|
	myName ::= if ⌊optionalName⌋ = 1
		then [optionalName[1]]
		else [defaultName's type];
	extend non-reentrant mutex with mutex name : myName
];

/**
 * A synchronization exception is raised when a {@type "synchronization device"}
 * is misused in some fashion.
 *
 * @type "synchronization exception"
 * @supertype "exception"
 * @category "Synchronization" "Exceptions"
 */
Public explicit class "synchronization exception" extends exception;

"reentered mutex" is a new field atom;
"reentering fiber" is a new field atom;

/**
 * A reentry exception is raised when an attempt is made to reenter a {@type
 * "non-reentrant mutex"}.
 *
 * @type "reentry exception"
 * @supertype "synchronization exception"
 * @category "Synchronization" "Exceptions"
 */
Public class "reentry exception" extends synchronization exception
	with fields
		public reentered mutex : non-reentrant mutex,
		public reentering fiber : fiber;

/**
 * Schedule the next {@type "fiber" waiter} to run, or simply release the
 * specified {@type "non-reentrant mutex" mutex} if there are no waiters.
 *
 * @method "Schedule next parked fiber or release_"
 * @param "mutex" "non-reentrant mutex"
 * @returns "⊤"
 * @category "Synchronization"
 */
Private method "Schedule next parked fiber or release_" is
[
	mutex : non-reentrant mutex
|
	queue ::= ↑mutex's parked fibers;
	/* Clear the ownership memento (for debugging clarity) and discard the
	 * current fiber from the queue. If no other fibers remain enqueued,
	 * then the lock is de facto released (the ownership memento is not
	 * canonical).
	 */
	mutex's owner := 0;
	lockReleased ::= dequeue current fiber from queue;
	/* If any fibers are queued, then grab the first one (without removing
	 * it from the queue), establish it as the owner, and unpark it.
	 */
	Unless lockReleased then
	[
		head ::= ↓queue[1];
		mutex's owner := head;
		Unpark head;
	];
] : ⊤;

/**
 * Acquire the {@type "non-reentrant mutex" mutex} or park until the mutex
 * becomes available.
 *
 * @method "Acquire_"
 * @param "mutex" "non-reentrant mutex"
 * @returns "⊤"
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Private method "Acquire_" is
[
	mutex : non-reentrant mutex
|
	/* Unconditionally add the current fiber to the parking queue. If the queue
	 * was empty at the time of enqueuing, then the current fiber now holds the
	 * lock.
	 */
	lockAcquired ::= priority enqueue current fiber on ↑mutex's parked fibers;
	/* If the lock was acquired, then note the current fiber as the owner. Note
	 * that this is an annotation; it is not canonical.
	 */
	If lockAcquired then
	[
		mutex's owner := current fiber;
	]
	/* Otherwise, park the current fiber. Loop until the mutex's ownership
	 * memento reflects the current fiber.
	 */
	else
	[
		Until mutex's owner = current fiber	do
		[
			Park current fiber;
			If termination requested then
			[
				Schedule next parked fiber or release mutex;
				Raise a termination-requested exception
			];
		];
	];
] : ⊤;

/**
 * Acquire the {@linkplain "non-reentrant mutex"}, then execute the specified
 * function as a critical section. {@type "fiber" Fibers} cannot concurrently
 * execute the critical section; access is serialized by the mutex, and only one
 * fiber may own the mutex at a given time.
 *
 * @method "Lock|lock_for_"
 * @param "mutex" "non-reentrant mutex"
 * @param "criticalSection" "[]→⊤"
 *        The critical section.
 * @returns "⊤"
 * @raises "reentry exception"
 *         If the owning fiber reenters the mutex.
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Public method "Lock|lock_for_" is
[
	mutex : non-reentrant mutex,
	criticalSection : []→⊤
|
	If mutex's owner = current fiber then
	[
		Raise a reentry exception with
			reentered mutex ::= mutex,
			reentering fiber ::= current fiber
	];
	Acquire mutex;
	/* The current fiber now holds the lock, so run the critical section. */
	Assert: mutex's owner = current fiber;
	Guard criticalSection
	ensure [Schedule next parked fiber or release mutex;];
] : ⊤;

Semantic restriction "Lock|lock_for_" is
[
	mutex : synchronization device's type,
	criticalSection : ([]→⊤)'s type
|
	criticalSection's return type
];

/**
 * A reentrant mutex is a mutual exclusion device that permits reentry.
 *
 * @type "reentrant mutex"
 * @supertype "non-reentrant mutex"
 * @category "Synchronization" "Data Abstractions" "Types"
 */
Public explicit class "reentrant mutex" extends non-reentrant mutex;

Method "“_”" is
[
	mutex : reentrant mutex
|
	format "‘typeDesc’ (‘myName’) [owner=‘owner’; parked=“parked”]" with
		typeDesc ::=
			(a|an) ““mutex's type's names” (as list partitioned by " ∩ ")”,
		myName ::= mutex's name,
		owner ::= mutex's owner's name,
		parked ::= map each f in mutex's parked fibers through [f's name]
];

/**
 * Construct and answer a {@type "reentrant mutex"}.
 *
 * @method "a reentrant mutex«named_»"
 * @param "myName" "nonempty string"
 *        If specified, then the name of the mutex, for improved diagnostics.
 *        If not specified, then {@code "«unnamed»"}.
 * @returns "reentrant mutex"
 * @category "Synchronization" "Data Abstractions" "Constructors"
 */
Public method "a reentrant mutex«named_»" is
[
	optionalName : nonempty string?
|
	myName ::= optionalName[1] else [defaultName];
	/* The ownership memento 0 is used to indicate that a mutex is unowned.
	 * It is non-canonical.
	 */
	a reentrant mutex with
		mutex name ::= myName,
		owner := 0,
		parked fibers := <>
] : reentrant mutex;

Semantic restriction "a reentrant mutex«named_»" is
[
	optionalName : nonempty string?'s type
|
	myName ::= if ⌊optionalName⌋ = 1
		then [optionalName[1]]
		else [defaultName's type];
	extend reentrant mutex with mutex name : myName
];

/**
 * Acquire the {@linkplain "reentrant mutex"}, then execute the specified
 * function as a critical section. {@type "fiber" Fibers} cannot concurrently
 * execute the critical section; access is serialized by the mutex, and only one
 * fiber may own the mutex at a given time. The owner may reacquire the lock
 * without incurring deadlock.
 *
 * @method "Lock|lock_for_"
 * @param "mutex" "reentrant mutex"
 * @param "criticalSection" "[]→⊤"
 *        The critical section.
 * @returns "⊤"
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Public method "Lock|lock_for_" is
[
	mutex : reentrant mutex,
	criticalSection : []→⊤
|
	/* If the current fiber already owns the mutex, then avoid putting it back
	 * onto the parking queue.
	 */
	shouldUnlock ::=
		if mutex's owner ≠ current fiber then [Acquire mutex; true]
		else [false];
	/* The current fiber now holds the lock, so run the critical section. */
	Assert: mutex's owner = current fiber;
	Guard criticalSection
	ensure
	[
		If shouldUnlock then [Schedule next parked fiber or release mutex;];
	];
] : ⊤;

"originating monitor" is a new field atom;
"condition predicate" is a new field atom;

/**
 * A condition represents a situation that may be signaled or awaited from
 * within a {@type "monitor"}. A condition maintains a {@type "parking queue"}
 * of {@type "fiber"}s that are waiting for the condition to be signaled.
 *
 * @type "condition"
 * @supertype "object"
 * @category "Synchronization" "Data Abstractions" "Types"
 */
Public class "condition" extends object
	with fields
		originating monitor : reentrant mutex,
		condition predicate : []→boolean
	with mutable field
		referable parked fibers : parking queue;

"signaler fibers" is a new field atom;

/**
 * A monitor is a {@type "reentrant mutex"} that supports {@type "condition"}s.
 *
 * @type "monitor"
 * @supertype "reentrant mutex"
 * @category "Synchronization" "Data Abstractions" "Types"
 */
Public class "monitor" extends reentrant mutex
	with mutable field
		referable signaler fibers : parking queue;

/* A condition's originating monitor is always a monitor, not merely a reentrant
 * mutex, but we couldn't mention the monitor previously because of definition
 * order.
 */
Semantic restriction "_'s⁇originating monitor" is
[
	aCondition : condition's type
|
	monitor
];

/**
 * Construct and answer a {@type "monitor"}.
 *
 * @method "a monitor named_"
 * @param "myName" "nonempty string"
 *        If specified, then the name of the monitor, for improved diagnostics.
 *        If not specified, then {@code "«unnamed»"}.
 * @returns "monitor"
 * @category "Synchronization" "Data Abstractions" "Constructors"
 */
Public method "a monitor«named_»" is
[
	optionalName : nonempty string?
|
	myName ::= optionalName[1] else [defaultName];
	/* The ownership memento 0 is used to indicate that a monitor is unowned.
	 * It is non-canonical.
	 */
	a monitor with
		mutex name ::= myName,
		owner := 0,
		parked fibers := <>,
		signaler fibers := <>
] : monitor;

Semantic restriction "a monitor«named_»" is
[
	optionalName : nonempty string?'s type
|
	myName ::= if ⌊optionalName⌋ = 1
		then [optionalName[1]]
		else [defaultName's type];
	extend monitor with mutex name : myName
];

/**
 * Schedule the next {@type "fiber" waiter} to run, giving preference to fibers
 * that previously signaled {@type "condition"}s. If no fibers are waiting, then
 * simply release the specified {@type "monitor" mutex}.
 *
 * @method "Schedule next parked fiber or release_"
 * @param "mutex" "monitor"
 * @returns "⊤"
 * @category "Synchronization"
 */
Private method "Schedule next parked fiber or release_" is
[
	mutex : monitor
|
	/* Clear the ownership memento (for debugging clarity) and discard the
	 * current fiber from the queue. If no other fibers remain enqueued,
	 * then the monitor is de facto released (the ownership memento is not
	 * canonical).
	 */
	mutex's owner := 0;
	queue ::= ↑mutex's parked fibers;
	signalers ::= ↑mutex's signaler fibers;
	/* If there are any signaler fibers waiting, then remove the first one from
	 * the queue and give it ownership of the monitor. Remove the current fiber
	 * from the parking queue.
	 */
	If ¬↓signalers is empty then
	[
		head ::= ↓signalers[1];
		signalers ?= ↓signalers[2..];
		Ignore: dequeue current fiber from queue;
		mutex's owner := head;
		Unpark head;
	]
	else
	[
		lockReleased ::= dequeue current fiber from queue;
		/* If any fibers are queued, then grab the first one (without removing
		 * it from the queue), establish it as the owner, and unpark it.
		 */
		Unless lockReleased then
		[
			head ::= ↓queue[1];
			mutex's owner := head;
			Unpark head;
		];
	];
];

/**
 * Construct and answer a {@type "condition"}.
 *
 * @method "a condition of_such that_"
 * @param "aMonitor" "monitor"
 *        The originating monitor.
 * @param "predicate" "[]→boolean"
 *        The predicate that should be asserted before signaling and after
 *        waking.
 * @returns "condition"
 * @category "Synchronization" "Constructors" "Data Abstractions"
 */
Public method "a condition of_such that_" is
[
	aMonitor : monitor,
	predicate : []→boolean
|
	a condition with
		originating monitor ::= aMonitor,
		condition predicate ::= predicate,
		parked fibers := <>
];

/**
 * Is the {@type "condition"} currently satisfied?
 *
 * @method "_is satisfied"
 * @param "aCondition" "condition"
 * @returns "boolean"
 *          {@method "true"} if the condition is satisfied, {@method "false"}
 *          otherwise.
 * @category "Synchronization" "Queries"
 */
Public method "_is satisfied" is
[
	aCondition : condition
|
	aCondition's condition predicate()
] : boolean;

"misused condition" is a new field atom;

/**
 * A condition exception indicates that a condition has been misused in some
 * fashion.
 *
 * @type "condition exception"
 * @supertype "synchronization exception"
 * @category "Synchronization" "Exceptions"
 */
Public class "condition exception" extends synchronization exception
	with field
		public misused condition : condition;

/**
 * A cannot-await-condition exception indicates that a condition was awaited
 * outside of a monitor.
 *
 * @type "cannot-await-condition exception"
 * @supertype "condition exception"
 * @category "Synchronization" "Exceptions"
 */
Public explicit class "cannot-await-condition exception"
extends condition exception;

/**
 * Wait until the specified {@type "condition"} is {@method "_is satisfied"
 * satisfied}. If the condition is satisfied already, then simply return control
 * to the caller. Otherwise, park the current fiber.
 *
 * @method "Await_"
 * @param "aCondition" "condition"
 * @returns "⊤"
 * @raises "cannot-await-condition exception"
 *         If the current fiber does not own the condition's {@type "monitor"}.
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Public method "Await_" is
[
	aCondition : condition
|
	/* If the current fiber is not the owner of the condition's originating
	 * monitor, then raise an appropriate exception.
	 */
	If aCondition's originating monitor's owner ≠ current fiber then
	[
		Raise a cannot-await-condition exception with
			misused condition ::= aCondition
	];
	/* If the predicate is true, then do nothing; simply return control to the
	 * caller. Otherwise, put the current fiber onto the condition's parking
	 * queue, schedule another fiber (or release the lock), and park.
	 */
	If ¬aCondition is satisfied then
	[
		mutex ::= aCondition's originating monitor;
		Ignore: priority enqueue current fiber on ↑aCondition's parked fibers;
		Schedule next parked fiber or release mutex;

		Until mutex's owner = current fiber do
			[Park current fiber, then honor a termination request;];

		/* The current fiber owns the lock again, and has been signaled that the
		 * condition is true. Assert this.
		 */
		Assert: aCondition is satisfied;
	];
] : ⊤;

/**
 * A cannot-signal-condition exception indicates that a condition was signaled
 * outside of a {@type "monitor"}.
 *
 * @type "cannot-signal-condition exception"
 * @supertype "condition exception"
 * @category "Synchronization" "Exceptions"
 */
Public explicit class "cannot-signal-condition exception"
extends condition exception;

/**
 * A condition-unsatisfied exception indicates that a condition was signaled
 * despite being unsatisfied.
 *
 * @type "condition-unsatisfied exception"
 * @supertype "condition exception"
 * @category "Synchronization" "Exceptions"
 */
Public explicit class "condition-unsatisfied exception"
extends condition exception;

/**
 * Signal that the specified {@type "condition"} is {@method "_is satisfied"
 * satisfied}. If any fiber is {@method "Await_" awaiting} this signal, then
 * schedule it to run and park the current fiber. A fiber blocked by this method
 * is given preference over one blocked by {@method "Lock|lock_for_"} during a
 * {@type "monitor"} transfer. If no fibers are awaiting this signal, then do
 * not block the current fiber; instead return control to the caller.
 *
 * @method "Signal_"
 * @param "aCondition" "condition"
 * @returns "⊤"
 * @raises "cannot-signal-condition exception"
 *         If the current fiber is not the owner of the condition's monitor.
 * @raises "condition-unsatisfied exception"
 *         If the condition is not actually satisfied.
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Public method "Signal_" is
[
	aCondition : condition
|
	/* If the current fiber is not the owner of the condition's originating
	 * monitor, then raise an appropriate exception.
	 */
	If aCondition's originating monitor's owner ≠ current fiber then
	[
		Raise a cannot-signal-condition exception with
			misused condition ::= aCondition
	];
	/* If the condition is not satisfied, then raise a condition-unsatisfied
	 * exception.
	 */
	If ¬aCondition is satisfied then
	[
		Raise a condition-unsatisfied exception with
			misused condition ::= aCondition
	];
	/* If any fibers are parked on this condition's parking queue, then remove
	 * the first one and give it ownership of the monitor.
	 */
	queue ::= ↑aCondition's parked fibers;
	If ¬↓queue is empty then
	[
		mutex ::= aCondition's originating monitor;
		/* Place the current fiber from onto the monitor's queue of signaler
		 * fibers, also leaving it on the main queue. This will give it
		 * preference during the next scheduling operation.
		 */
		Ignore: priority enqueue current fiber on ↑mutex's signaler fibers;
		head ::= ↓queue[1];
		queue ?= ↓queue[2..];
		/* Place the next fiber back onto the monitor's main queue. */
		Ignore: priority enqueue head on ↑mutex's parked fibers;
		mutex's owner := head;
		Unpark head;
		/* Park the current fiber. */
		Until mutex's owner = current fiber do
			[Park current fiber, then honor a termination request;];
	];
] : ⊤;

/**
 * Signal iff the specified {@type "condition"} is {@method "_is satisfied"
 * satisfied}. If any fiber is {@method "Await_" awaiting} this signal, then
 * schedule it to run and park the current fiber. A fiber blocked by this method
 * is given preference over one blocked by {@method "Lock|lock_for_"} during a
 * {@type "monitor"} transfer. If no fibers are awaiting this signal, then do
 * not block the current fiber; instead return control to the caller.
 *
 * @method "Signal_if satisfied"
 * @param "aCondition" "condition"
 * @returns "⊤"
 * @raises "cannot-signal-condition exception"
 *         If the current fiber is not the owner of the condition's monitor.
 * @raises "condition-unsatisfied exception"
 *         If the condition is not actually satisfied.
 * @category "Synchronization"
 */
Public method "Signal_if satisfied" is
[
	aCondition : condition
|
	If aCondition is satisfied then [Signal aCondition;];
] : ⊤;

"maximum permits" is a new field atom;
"available permits" is a new field atom;
"internal monitor" is a new field atom;
"availability" is a new field atom;

/**
 * A semaphore is a {@type "synchronization device"} that describes how
 * many of a restricted resource are currently available.
 *
 * @type "semaphore"
 * @supertype "synchronization device"
 * @category "Synchronization" "Types" "Data Abstractions"
 */
Public class "semaphore" extends synchronization device
	with fields
		mutex name : nonempty string ("_'s⁇name"),
		public maximum permits : [0..∞],
		internal monitor : monitor,
		availability : condition
	with mutable field
		referable available permits : extended integer;

Semantic restriction "_'s⁇available permits" is
[
	sem : semaphore's type
|
	sem's maximum permits
];

Semantic restriction "_'s⁇available permits:=_" is
[
	sem : semaphore's type,
	permits : extended integer's type
|
	If ¬permits ⊆ sem's maximum permits then
	[
		Reject parse, expected:
			format
				"new count of available permits ⊆ “①” \
				\|(but “②” is not a subtype)"
			with sem's maximum permits, permits
	];
	⊤
];

/**
 * A counting semaphore is a {@type "synchronization device"} that describes how
 * many of a restricted resource are currently available.
 *
 * @type "counting semaphore"
 * @supertype "synchronization device"
 * @category "Synchronization" "Types" "Data Abstractions"
 */

Public method "counting semaphore" is [semaphore];

/**
 * A binary semaphore supports but a single permit, and it is therefore suitable
 * for use as a mutual exclusion device.
 *
 * @type "binary semaphore"
 * @supertype "semaphore"
 * @category "Synchronization" "Types" "Data Abstractions"
 */
Public class "binary semaphore" extends semaphore
	with field
		maximum permits : [0..1];

/**
 * Construct and answer a {@type "semaphore"}.
 *
 * @method "a semaphore«named_»with_«out of_»permit|permits"
 * @param "optionalName" "nonempty string?"
 *        If specified, then the name of the semaphore, for improved
 *        diagnostics. If not specified, then {@code "«unnamed»"} is used.
 * @param "permits" "[0..∞]"
 *        The number of permits initially available.
 * @param "optionalMaximumPermits" "[0..∞]?"
 *        If specified, then the maximum number of permits supported by the new
 *        semaphore. If not specified, then {@method "∞"}.
 * @returns "semaphore"
 * @category "Synchronization" "Constructors" "Data Abstractions"
 */
Public method "a semaphore«named_»with_«out of_»permit|permits" is
[
	optionalName : nonempty string?,
	permits : [0..∞],
	optionalMaximumPermits : [0..∞]?
|
	myName ::= optionalName[1] else [defaultName];
	maximumPermits ::= optionalMaximumPermits[1] else [∞];
	mutex ::= a monitor named "monitor for semaphore named " ++ myName;
	/* Create a variable explicitly so that the condition can reference it. */
	availablePermits : extended integer := permits;
	cond ::= a condition of mutex such that [availablePermits > 0];
	Assert: permits ≤ maximumPermits;
	a semaphore with
		mutex name ::= myName,
		maximum permits ::= maximumPermits,
		internal monitor ::= mutex,
		availability ::= cond,
		available permits ::= ↑availablePermits
] : semaphore;

Semantic restriction "a semaphore«named_»with_«out of_»permit|permits" is
[
	optionalName : nonempty string?'s type,
	permits : [0..∞]'s type,
	optionalMaximumPermits : [0..∞]?'s type
|
	myName ::= if ⌊optionalName⌋ = 1
		then [optionalName[1]]
		else [defaultName's type];
	maximumPermits ::= if ⌊optionalMaximumPermits⌋ = 1
		then [optionalMaximumPermits[1]]
		else [∞'s type];
	extend semaphore with
		mutex name : myName,
		maximum permits : maximumPermits
];

/**
 * Wait until the specified {@type "semaphore"} has an available permit. If a
 * permit is already available, then consume it immediately and return control
 * to the caller. Otherwise, park the current fiber.
 *
 * @method "Await_"
 * @param "sem" "semaphore"
 * @returns "⊤"
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Method "Await_" is
[
	sem : semaphore
|
	Lock sem's internal monitor for
	[
		If sem's available permits = 0 then [Await sem's availability;];
		Assert: sem's available permits > 0;
		↑sem's available permits--;
	];
] : ⊤;

/**
 * Wait until the specified number of permits become available from the {@type
 * "semaphore"}.
 *
 * @method "Await__time|times"
 * @param "sem" "semaphore"
 * @param "permits" "whole number"
 *        The number of permits to consume.
 * @returns "⊤"
 * @see {@method "Await_"}
 * @category "Synchronization"
 */
Public method "Await__time|times" is
[
	sem : semaphore,
	permits : whole number
|
	Repeat [Await sem;] permits times;
] : ⊤;

"misused semaphore" is a new field atom;

/**
 * A semaphore exception indicates that a semaphore has been used
 * inappropriately.
 *
 * @type "semaphore exception"
 * @supertype "synchronization exception"
 * @category "Synchronization" "Exceptions"
 */
Public class "semaphore exception" extends synchronization exception
	with field
		public misused semaphore : semaphore;

/**
 * A cannot-exceed-maximum-permits exception is raised by {@method "Signal_"}
 * when the specified semaphore already has the maximum number of permits.
 *
 * @type "cannot-exceed-maximum-permits exception"
 * @supertype "semaphore exception"
 * @category "Synchronization" "Exceptions"
 */
Public explicit class "cannot-exceed-maximum-permits exception"
extends semaphore exception
	with field
		public misused semaphore : semaphore;

/**
 * Release a permit back to the specified {@type "semaphore"}. If any fibers are
 * {@method "Await_" awaiting} a permit, then schedule one to run and park the
 * current fiber. A fiber blocked by this method is given preference over one
 * blocked by {@method "Await_"}. If no fibers are awaiting the release of a
 * permit, then do not block the current fiber; instead return control to the
 * caller.
 *
 * @method "Signal_"
 * @param "sem" "semaphore"
 * @returns "⊤"
 * @raises "cannot-exceed-maximum-permits exception"
 *         If releasing a permit would exceed the maximum number of permits
 *         allowed by the specified semaphore.
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Method "Signal_" is
[
	sem : semaphore
|
	Lock sem's internal monitor for
	[
		If sem's available permits = sem's maximum permits then
		[
			Raise a cannot-exceed-maximum-permits exception
				with misused semaphore ::= sem
		];
		↑sem's available permits++;
		Signal sem's availability;
	];
] : ⊤;

/**
 * Release the specified number of permits back to the {@type "semaphore"}. If
 * any fibers are {@method "Await_" awaiting} a permit, then schedule as many to
 * run as permits have been released and park the current fiber. A fiber blocked
 * by this method is given preference over one blocked by {@method "Await_"}. If
 * no fibers are awaiting the release of a permit, then do not block the current
 * fiber; instead return control to the caller. If not all permits can be
 * released, then release as many as possible before raising a {@type
 * "cannot-exceed-maximum-permits exception"}.
 *
 * @method "Signal__time|times"
 * @param "sem" "semaphore"
 * @param "permits" "whole number"
 *        The number of permits to release.
 * @returns "⊤"
 * @raises "cannot-exceed-maximum-permits exception"
 *         If releasing a permit would exceed the maximum number of permits
 *         allowed by the specified semaphore.
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @see {@method "Signal_"}
 * @category "Synchronization"
 */
Public method "Signal__time|times" is
[
	sem : semaphore,
	permits : whole number
|
	Lock sem's internal monitor for
	[
		If sem's available permits + permits > sem's maximum permits then
		[
			sem's available permits := sem's maximum permits;
			Raise a cannot-exceed-maximum-permits exception
				with misused semaphore ::= sem
		];
		sem's available permits := sem's available permits + permits;
		Signal sem's availability;
	];
] : ⊤;

/**
 * {@method "Await_" Acquire a permit} from the {@type "semaphore"}, then
 * execute the specified function (as a critical section), then {@method
 * "Signal_" release the permit}. If the semaphore is a {@type
 * "binary semaphore"}, then multiple {@type "fiber"}s cannot concurrently
 * execute the critical section; access is serialized. For a general {@type
 * "counting semaphore"}, as may fibers as there are available permits are
 * permitted to occupy the critical section concurrently.
 *
 * @method "Lock|lock_for_"
 * @param "sem" "semaphore"
 * @param "criticalSection" "[]→⊤"
 *        The critical section.
 * @returns "⊤"
 * @raises "termination-requested exception"
 *         If {@method "termination requested" termination has been requested}.
 * @category "Synchronization"
 */
Method "Lock|lock_for_" is
[
	sem : semaphore,
	criticalSection : []→⊤
|
	Await sem;
	Guard criticalSection
	ensure [Signal sem;];
] : ⊤;

/**
 * @method "a|an_«with«_«:»?:=_‡,»»"
 * @forbids "1" "binary semaphore" "condition" "counting semaphore" "monitor"
 *              "non-reentrant mutex" "reentrant mutex" "semaphore"
 */
Grammatical restriction "a|an_«with«_«:»?:=_‡,»»" is
	<{
		"binary semaphore",
		"condition",
		"counting semaphore",
		"monitor",
		"non-reentrant mutex",
		"reentrant mutex",
		"semaphore"
	}, ∅, ∅>;
