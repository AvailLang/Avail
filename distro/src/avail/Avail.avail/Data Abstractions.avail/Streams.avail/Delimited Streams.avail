/*
 * Delimited Streams.avail
 * Copyright © 1993-2015, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Delimited Streams"
Versions
	"1.0.0 DEV 2014-04-28"
Uses
	"Abstract Streams",
	"Foundation" = (-"_'s⁇element type", …)
Names
	"_delimited by_",
	"_delimited by any of_"
Body

Private method "delimiter|delimiters" is [$"delimiter|delimiters"];

/**
 * A delimited stream answers tuples of elements from its decorated stream.
 * These tuples are separated by occurrences of the delimiter.
 *
 * @type "delimited stream"
 * @supertype "stream"
 * @field "element type" "tuple's type"
 * @field "reconstructible decorated stream" "stream"
 * @field "delimiter" "any+"
 * @category "Data Abstractions" "Streams"
 */
Class "delimited stream" extends stream
	with fields
		element type : tuple's type,
		reconstructible decorated stream : stream,
		delimiter : any+;

Method "_is empty" is
[
	aStream : delimited stream
|
	aStream's decorated stream is empty
] : boolean;

delimited stream's head method is
[
	aStream : delimited stream
|
	head : tuple := <>;
	rest : stream := aStream's decorated stream;
	Until rest is empty ∨ rest starts with aStream's delimiter do
	[
		head := eject head ++ <rest's head>;
		rest := eject rest's tail;
	];
	head
] : tuple;

delimited stream's tail method is
[
	aStream : delimited stream
|
	rest : stream := aStream's decorated stream;
	Until rest is empty ∨ rest starts with aStream's delimiter do
	[
		rest := eject rest's tail;
	];
	If ¬rest is empty then
	[
		rest := drop |aStream's delimiter| from eject rest;
	];
	aStream's decorated stream ::= rest
] : delimited stream;

/**
 * Construct and answer a stream that produces tuples of values from `aStream`.
 * These tuples are separated by occurrences of `delimiterTuple` (which will
 * never be produced as output from the answered stream).
 * 
 * @method "_delimited by_"
 * @param "aStream" "stream"
 *    aStream - A stream.
 * @param "delimiterTuple" "any+"
 *    delimiterTuple - The delimiter.
 * @returns "stream of tuple"
 *    The requested stream.
 * @category "Data Abstractions" "Streams"
 */
Public method "_delimited by_" is
[
	aStream : stream,
	delimiterTuple : any+
|
	a delimited stream with
		element type ::= <aStream's element type…|>,
		decorated stream ::= aStream,
		delimiter ::= delimiterTuple
] : stream of tuple;

Semantic restriction "_delimited by_" is
[
	streamType : stream's type,
	delimiterType : any+'s type
|
	extend delimited stream with
		element type : <streamType's element type's instance…|>'s type,
		decorated stream : streamType,
		delimiter : delimiterType
];

/**
 * A delimited stream answers tuples of elements from its decorated stream.
 * These tuples are separated by occurrences of any delimiter.
 *
 * @type "multiply delimited stream"
 * @supertype "delimited stream"
 * @field "delimiters" "<any+…|1..>;"
 * @category "Data Abstractions" "Streams"
 */
Explicit class "multiply delimited stream" extends delimited stream
	with fields
		delimiters : <any+…|1..>;

multiply delimited stream's head method is
[
	aStream : multiply delimited stream
|
	head : tuple := <>;
	rest : stream := aStream's decorated stream;
	Until rest is empty ∨ any delimiter of aStream's delimiters satisfies
		[rest starts with delimiter]
	do
	[
		head := eject head ++ <rest's head>;
		rest := eject rest's tail;
	];
	head
] : tuple;

multiply delimited stream's tail method is
[
	aStream : multiply delimited stream
|
	rest : stream := aStream's decorated stream;
	theDelimiter : tuple;
	Until
		if rest is empty then [ true ]
		else
		[
			index ::= first index of aStream's delimiters where
				[ delimiter : tuple | rest starts with delimiter ];
			If index ≠ 0 then
			[
				nonzero ::= cast index into [ t : natural number | t ];
				theDelimiter := aStream's delimiters[nonzero];
			];
			theDelimiter is assigned
		]
	do
	[
		rest := eject rest's tail;
	];
	If ¬rest is empty then
	[
		rest := drop |theDelimiter| from eject rest;
	];
	aStream's decorated stream ::= rest
] : multiply delimited stream;

/**
 * Construct and answer a stream that produces tuples of values from `aStream`.
 * These tuples are separated by occurrences of `delimiterTuple` (which will
 * never be produced as output from the answered stream).
 * 
 * @method "_delimited by any of_"
 * @param "aStream" "stream"
 *    aStream - A stream.
 * @param "delimitersTuple" "<any+…|1..>"
 *    delimiterTuple - The delimiter.
 * @returns "stream of tuple"
 *    The requested stream.
 * @category "Data Abstractions" "Streams"
 */
Public method "_delimited by any of_" is
[
	aStream : stream,
	delimitersTuple : <any+…|1..>
|
	a multiply delimited stream with
		element type ::= <aStream's element type…|>,
		decorated stream ::= aStream,
		delimiters ::= delimitersTuple
] : stream of tuple;

Semantic restriction "_delimited by any of_" is
[
	streamType : stream's type,
	delimitersType : <any+…|1..>'s type
|
	extend multiply delimited stream with
		element type : <streamType's element type's instance…|>'s type,
		decorated stream : streamType,
		delimiters : delimitersType
];
