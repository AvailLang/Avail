/*
 * Optional.avail
 * Copyright © 1993-2019, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "Optional"
Versions
	"1.4.0"
Uses
	"Foundation",
	"Unit Testing"
Names
	/* Optional. */
	"optional_",
		/* subclasses */
		"absent",
			/* constructors */
			"none",
		"present_",
			/* constructors */
			"``_",
		/* methods */
		"_'s⁇value",

	/* Control structures. */
	"When_is present do_",
	"When|when_is present do_else_",

	/* Test suites. */
	"optional test suite"
Body

"actual value" is a new atom;

/**
 * The null antipattern is generally to be avoided, because it effectively
 * asserts the existence of an instance of the bottom type. There are times,
 * however, when the presence of a value should be structurally optional. The
 * {@type "optional"} type satisfies this niche. Its concrete subtypes,
 * {@type "present"} and {@type "absent"}, indicate the presence and absence of
 * a value, respectively.
 *
 * What this facility offers beyond what {@type "any?"} offers is 1) a more
 * first-order mechanism and 2) another dispatch opportunity for cases where
 * {@type "tuple"} must be used generally and cannot be understood specially in
 * its {@type "any?"} specialization.
 *
 * @type "optional"
 */
Explicit class "optional" extends object
	with field
		actual value : any?;

/**
 * Construct and answer an {@type "optional"} type.
 *
 * @method "optional_"
 * @param "valueType" "any's type"
 *        The type of value accessible from instances of {@type "present"}
 *        subtypes of the answer.
 * @returns "optional's type"
 *          An {@type "optional"} type parameterized by the argument.
 */
Public stable method "optional_" is
[
	valueType : any's type
|
	extend optional with actual value : valueType?
] : optional's type;

/**
 * @method "optional_"
 * @restricts "any's type's type"
 */
Semantic restriction "optional_" is
[
	valueMeta : any's type's type
|
	(extend optional with actual value : valueMeta's instance?)'s type
];

/**
 * {@type "present"} is an {@type "optional"} that actually contains a value.
 *
 * @type "present"
 * @supertype "optional"
 */
Explicit class "present" extends optional
	with fields
		actual value : <any…|1>;

/**
 * Construct and answer a {@type "present"} type.
 *
 * @method "present_"
 * @param "valueType" "any's type"
 *        The type of value accessible from instances of subtypes of the answer.
 * @returns "present's type"
 *          A {@type "present"} type parameterized by the argument.
 */
Public stable method "present_" is
[
	valueType : any's type
|
	extend present with actual value : <valueType…|1>
] : present's type;

/**
 * @method "present_"
 * @restricts "any's type's type"
 */
Semantic restriction "present_" is
[
	valueMeta : any's type's type
|
	(extend present with actual value : <valueMeta's instance…|1>)'s type
];

/**
 * Construct and answer a {@type "present"} that contains {@param "value"}.
 *
 * @method "``_"
 * @param "value" "any"
 *        A value.
 * @returns "present"
 *          A {@type "present"} that contains {@param "value"}.
 */
Public stable method "``_" is
[
	value : any
|
	a present with actual value ::= <value>
] : present;

/**
 * @method "``_"
 * @restricts "any's type"
 */
Semantic restriction "``_" is
[
	valueType : any's type
|
	extend present with actual value : <valueType…|1>
];

/**
 * {@type "absent"} is an {@type "optional"} that does not contain a value.
 *
 * @type "absent"
 * @supertype "optional"
 */
Public explicit class "absent" extends optional
	with fields
		actual value : <⊥…|0>;

_none ::= an absent with actual value ::= <>;

/**
 * Answer the canonical instance of {@type "absent"}.
 *
 * @method "none"
 * @returns "absent"
 *          An {@type "absent"}.
 */
Public stable method "none" is [_none];

/**
 * Answer the value encapsulated by the argument.
 *
 * @method "_'s⁇value"
 * @param "anOptional" "optional"
 *        An {@type "optional"}.
 * @returns "any"
 *          The value encapsulated by the argument.
 */
Public stable abstract method "_'s⁇value" is [optional]→any;

/**
 * Reject the current parse if the argument type is not statically known to be
 * a subtype of {@type "present"}.
 *
 * @method "_'s⁇value"
 * @restricts "optional's type"
 */
Semantic restriction "_'s⁇value" is
[
	optionalType : optional's type
|
	If optionalType ⊆ absent then
	[
		Reject parse, expected:
			format "value extraction not to be attempted on “①”"
			with optionalType
	];
	Unless optionalType ⊆ present then
	[
		Reject parse, expected:
			format
				"value extraction not to be attempted on “①” \
				\|(try casting to “②”)"
			with optionalType, present (optionalType's actual value)
	];
	optionalType's actual value[1]
];

/**
 * Answer the value encapsulated by the argument.
 *
 * @method "_'s⁇value"
 * @param "someValue" "present"
 *        {@type "present" Some} value.
 * @returns "any"
 *          The encapsulated value.
 */
Method "_'s⁇value" is
[
	someValue : present
|
	someValue's actual value[1]
] : any;

/**
 * Answer the value encapsulated by the argument.
 *
 * @method "_'s⁇value"
 * @param "someValue" "present"
 *        {@type "present" Some} value.
 * @returns "any"
 *          The encapsulated value.
 */
Method "_'s⁇value" is
[
	noValue : absent
|
	Shouldn't happen ("synthetic call extracted a value from an absent")
] : any;

/**
 * Apply the specified function to the value contained in {@param "anOptional"},
 * but only if {@param "anOptional"} is a {@type "present"}.
 *
 * @method "When_is present do_"
 * @param "anOptional" "optional"
 *        An {@type "optional"}.
 * @param "presentBlock" "[⊥]→⊤"
 *        The function that should be applied to {@param "anOptional"}'s
 *        content if {@param "anOptional"} is an instance of {@param "present"}.
 * @returns "⊤"
 */
Public method "When_is present do_" is
[
	anOptional : optional,
	presentBlock : [⊥]→⊤
|
	Cast anOptional
		into [aPresent : present | presentBlock(aPresent's value);]
		else [/* Do nothing. */];
] : ⊤;

/**
 * @method "When_is present do_"
 * @restricts "optional's type"
 * @restricts "[⊥]→⊤'s type"
 */
Semantic restriction "When_is present do_" is
[
	optionalType : optional's type,
	presentBlock : [⊥]→⊤'s type
|
	Unless optionalType's actual value[1] ⊆ presentBlock[1] then
	[
		Reject parse, expected:
			format "function to accept “①” (but it only accepts “②”)"
			with optionalType's actual value[1], presentBlock[1]
	];
	⊤
];

/**
 * Apply the specified function to the value contained in {@param "anOptional"},
 * but only if {@param "anOptional"} is a {@type "present"}. Otherwise, apply
 * {@param "elseBlock"}.
 *
 * @method "When_is present do_"
 * @param "anOptional" "optional"
 *        An {@type "optional"}.
 * @param "presentBlock" "[⊥]→⊤"
 *        The function to apply to {@param "anOptional"}'s content if {@param
 *        "anOptional"} is an instance of {@param "present"}.
 * @param "elseBlock" "[]→⊤"
 *        The function to apply if {@param "anOptional"} is an instance of
 *        {@param "absent"}.
 * @returns "⊤"
 *          The value, if any, produced by the chosen function.
 */
Public method "When|when_is present do_else_" is
[
	anOptional : optional,
	presentBlock : [⊥]→⊤,
	elseBlock : []→⊤
|
	cast anOptional
		into [aPresent : present | presentBlock(aPresent's value);]
		else elseBlock;
] : ⊤;

/**
 * @method "When|when_is present do_else_"
 * @restricts "optional's type"
 * @restricts "[⊥]→⊤'s type"
 * @restricts "[]→⊤'s type"
 */
Semantic restriction "When|when_is present do_else_" is
[
	optionalType : optional's type,
	presentBlock : [⊥]→⊤'s type,
	elseBlock : []→⊤'s type
|
	Unless optionalType's actual value[1] ⊆ presentBlock[1] then
	[
		Reject parse, expected:
			format "function to accept “①” (but it only accepts “②”)"
			with optionalType's actual value[1], presentBlock[1]
	];
	if optionalType ⊆ present then [presentBlock's return type]
	else if optionalType ⊆ absent then [elseBlock's return type]
	else [presentBlock's return type ∪ elseBlock's return type]
];
