/*
 * UTF8 Converters.avail
 * Copyright © 1993-2021, The Avail Foundation, LLC.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * * Redistributions of source code must retain the above copyright notice, this
 *   list of conditions and the following disclaimer.
 *
 * * Redistributions in binary form must reproduce the above copyright notice,
 *   this list of conditions and the following disclaimer in the documentation
 *   and/or other materials provided with the distribution.
 *
 * * Neither the name of the copyright holder nor the names of the contributors
 *   may be used to endorse or promote products derived from this software
 *   without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */

Module "UTF-8 Converters"
Versions
	"Avail-1.6.0"
Uses
	"Abstract Converters",
	"Foundation",
	"Iterators",
	"Converting Reader"
Names
	"a UTF8 decoder",
	"a UTF8 encoder",
	"UTF8 decoder",
	"UTF8 encoder",
	"_→UTF8",
	"“_”(UTF8)"
Body

"residue" is a new field atom;

/**
 * Produce a sequence of bytes that constitute the UTF-8 encoding of the given
 * Unicode code point.
 *
 * @method "bytes from_"
 * @param "codePoint" "code point"
 * @returns "byte*"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Private abstract method "bytes from_" is [code point] → byte*;

Private method "bytes from_" is
[
	codePoint : [0..127]
|
	<codePoint>
];

Private method "bytes from_" is
[
	codePoint : [128..2047]
|
	<192 + (codePoint >> 6),
	 128 + (codePoint bit∧ 63)>
] : byte*;

Private method "bytes from_" is
[
	codePoint : [2048..65535]
|
	<224 + (codePoint >> 12),
	 128 + ((codePoint >> 6) bit∧ 63),
	 128 + (codePoint bit∧ 63)>
] : byte*;

Private method "bytes from_" is
[
	codePoint : [65536..1114111]
|
	<240 + (codePoint >> 18),
	 128 + ((codePoint >> 12) bit∧ 63),
	 128 + ((codePoint >> 6) bit∧ 63),
	 128 + (codePoint bit∧ 63)>
] : byte*;

/**
 * A converter that translates a tuple of characters (i.e., a string) into a
 * tuple of UTF-8 encoded bytes.
 *
 * @type "UTF8 encoder"
 * @supertype "converter"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Public explicit class "UTF8 encoder"
	extends converter from character to byte;

/* Compute [character]→byte statically, so we have a strong enough argument
 * type.  That's because of contravariance of argument types, and the fact that
 * the expression "character" is theoretically allowed to produce something more
 * specific than the character type.
 */
charToByte ::= [character]→byte;

/**
 * Create a {@type "UTF8 encoder"}.
 *
 * @method "a UTF8 encoder"
 * @returns "UTF8 encoder"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Public method "a UTF8 encoder" is
[
	a UTF8 encoder with conversion type ::= charToByte
];

/**
 * Encode some characters into UTF-8 bytes.
 *
 * @method "_process_"
 * @param "encoder" "UTF8 encoder"
 * @param "sourceTuple" "string"
 * @returns "byte*"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Method "_process_" is
[
	encoder : UTF8 encoder,
	sourceTuple : string
|
	output : byte* := <>;
	For each c of sourceTuple do
	[
		output := eject output ++ bytes from c's code point;
	];
	output
];

/**
 * Flushing a UTF-8 encoder always produces an empty sequence of bytes.
 *
 * @method "flush_"
 * @param "encoder" "UTF8 encoder"
 * @returns "empty tuple"
 * @category "Data Abstractions" "Converters" "Codecs"
 */
Method "flush_" is
[
	encoder : UTF8 encoder
|
	<>
];

/**
 * A converter that translates a tuple of UTF-8 encoded bytes into a tuple of
 * characters (i.e., a string).
 *
 * @type "UTF8 decoder"
 * @supertype "converter"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Public explicit class "UTF8 decoder"
	extends converter from byte to character
	with mutable field
		referable residue : byte*;

/* Compute [byte]→character statically, so we have a strong enough argument
 * type.  That's because of contravariance of argument types, and the fact that
 * the expression "byte" is theoretically allowed to produce something more
 * specific than the byte type.
 */
byteToChar ::= [byte]→character;

Public method "a UTF8 decoder" is
[
	a UTF8 decoder with
		conversion type ::= byteToChar,
		residue := <>
];

/**
 * Answer how many bytes long a UTF-8 sequence starting with the given byte will
 * be.  Note that some leading bytes are always illegal, and some leading bytes
 * may or may not be illegal, depending on the continuation bytes (for example,
 * they may produce an "overlong encoding").  We only detect leading bytes of
 * always-illegal sequences here, answering zero.
 *
 * @method "byte count for_"
 * @param "leadingByte" "byte"
 * @returns "[0..4]"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Private method "byte count for_" is [leadingByte : byte | 0] : [0..4];
Private method "byte count for_" is [leadingByte : [0..127] | 1];
Private method "byte count for_" is [leadingByte : [194..223] | 2];
Private method "byte count for_" is [leadingByte : [224..239] | 3];
Private method "byte count for_" is [leadingByte : [240..244] | 4];


/**
 * Decode some UTF-8 bytes into a Unicode string.  Unrecognized byte sequences
 * will be converted into the Unicode replacement character (U+FFFD).
 * An incomplete byte sequence will simply wait until more data is supplied or a
 * flush occurs (which will cause U+FFFD to be emitted for the truncated
 * buffered content).
 *
 * @method "_process_"
 * @param "decoder" "UTF8 decoder"
 * @returns "bytes" "byte*"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Method "_process_" is
[
	decoder : UTF8 decoder,
	bytes : byte*
|
	in : byte* := decoder's residue ++ bytes;
	out : string := "";
	index : natural number := 1;
	leadByte : byte;
	count : [0..4];
	While
		index ≤ |in| ∧ [
			leadByte := in[index];
			count := byte count for leadByte;
			index + count - 1 ≤ |in|
		]
	do
	[
		continuationBytes : byte*;
		If count > 0 ∧
			[
				continuationBytes := in[index + 1 .. index + count - 1];
				continuationBytes ∈ <[128..191]…|>] then
		[
			/* Extract the bytes and decode them. */
			accumulator : whole number := leadByte bit∧ (255 >> count);
			index++;
			For each nextByte of continuationBytes do
			[
				accumulator := (eject accumulator << 6) + (nextByte bit∧ 63);
				index++;
			];
			out := eject out ++ (cast accumulator
				into [cp : code point | <cp → character>]
				else ["\(fffd)"]);
		]
		else
		[
			/* The continuation bytes are invalid.  For now, decode under the
			 * assumption that the leading byte correctly indicates the number
			 * of continuation bytes, but one of the continuation bytes is
			 * wrong.  Replace the sequence with the Unicode replacement
			 * character (U+FFFD), and skip the leading and continuation bytes.
			 */
			out := eject out ++ "\(fffd)";
			index := index + count + 1;
		];
	];
	decoder's residue := in[index..];
	out
];

/**
 * Flush the UTF-8 decoder.  Since a complete sequence of bytes representing a
 * character would have already produced a corresponding character, it must be
 * the case that a non-empty residue should be treated as an invalid sequence,
 * for which a single Unicode replacement character (U+FFFD) will be emitted.
 *
 * @method "flush_"
 * @param "decoder" "UTF8 decoder"
 * @returns "bytes" "byte*"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Method "flush_" is
[
	decoder : UTF8 decoder
|
	out ::= if decoder's residue = <> then [""] else ["\(fffd)"];
	decoder's residue := <>;
	out
];

/**
 * Convert {@param "s"} to a UTF-8 encoded {@type "tuple"} of {@type "byte"}s.
 *
 * @method "_→UTF8"
 * @param "s" "string"
 * @returns "byte*"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Public stable method "_→UTF8" is
[
	s : string
|
	take at most ∞ from a reader over s converted by a UTF8 encoder
] : byte*;

/**
 * Convert the specified UTF-8 encoded {@type "tuple"} of {@type "byte"}s into a
 * {@type "string"}.
 *
 * @method "“_”(UTF8)"
 * @param "t" "byte*"
 * @returns "string"
 * @category "Data Abstractions" "Converters" "Codecs" "UTF-8"
 */
Public stable method "“_”(UTF8)" is
[
	t : byte*
|
	a UTF8 decoder process t
] : string;

/**
 * Protect the constructor expressions from potentially ambiguous
 * interpretations.
 *
 * @method "a|an_«with«_«:»?:=_‡,»»"
 * @forbids 1 "UTF8 encoder"
 * @forbids 1 "UTF8 decoder"
 */
Grammatical restriction {"a|an_«with«_«:»?:=_‡,»»"} is
<
	{"UTF8 encoder", "UTF8 decoder"},
	∅,
	∅
>;
